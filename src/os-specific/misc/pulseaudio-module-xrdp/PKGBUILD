# Credits Athena OS ( https://athenaos.org )

# Contributor: Jat <chat@jat.email>

pkgname=pulseaudio-module-xrdp
pkgver=0.8
pkgrel=1
pkgdesc="xrdp pulseaudio module"
url="https://github.com/neutrinolabs/pulseaudio-module-xrdp"
license=('LGPL-2.1-only')
arch=('i686' 'x86_64' 'armv6h' 'armv7l' 'aarch64')

makedepends=(
  'check'
  'git'
  'libpulse'
  'meson'
  'tdb'
)
install="$pkgname.install"
_pkgsrc="$pkgname-$pkgver"
_pkgsrc_pulse="pulseaudio.gitlab"
_pkgext="tar.gz"
source=(
  "$_pkgsrc.$_pkgext"::"$url/archive/refs/tags/v${pkgver}.$_pkgext"
  "$_pkgsrc_pulse"::"git+https://gitlab.freedesktop.org/pulseaudio/pulseaudio.git"
)
sha256sums=('f0cc39b67421511c28395329a4091f8c5be79d861414b7d67313aa760e3b9db4'
            'SKIP')

_build_pulse() (
  echo "Building pulseaudio..."

  local _pulseaudio_ver _ref
  _pulseaudio_ver=$(LANG=C LC_ALL=C pacman -Si pulseaudio | grep -Pom1 '^Version\s+:\s+\K(\S+)-[0-9\.]+')
  if grep -qm1 '+' <<< "$_pulseaudio_ver"; then
    _ref=$(sed -E 's&^\S+[+]g([a-f0-9]+)-\S+$&\1&' <<< ${_pulseaudio_ver})
  else
    _ref=v$(sed -E 's&^([0-9]+\.[0-9]+).*$&\1&' <<< ${_pulseaudio_ver})
  fi

  cd "$_pkgsrc_pulse"
  git -c advice.detachedHead=false checkout -f "${_ref:?}"

  local _meson_options=(
    -Ddoxygen=false
    -Dtests=false
  )

  meson build "${_meson_options[@]}"
)

_build_plugin() (
  echo "Building pulseaudio-module-xrdp..."

  cd "$_pkgsrc"
  sed -i '\#-I $(PULSE_DIR)/src#a -I $(PULSE_DIR)/build \\' src/Makefile.am

  autoreconf -ivf
  ./configure "PULSE_DIR=$srcdir/$_pkgsrc_pulse" --prefix='/usr' --libexecdir='/usr/lib'
  make
)

build() {
  _build_pulse
  _build_plugin
}

package() {
  depends=(
    'pulseaudio'
    'xrdp'
  )

  cd "$_pkgsrc"
  make DESTDIR="$pkgdir" install
}

