pkgname=mitre-attack-navigator
pkgver=10.efb4c43
pkgrel=1
pkgdesc="A Tauri-based desktop app for MITRE's ATT&CK Navigator cybersecurity framework."
arch=('x86_64' 'aarch64')
url='https://github.com/Athena-OS/mitre-attack-navigator'
license=('GPL-3.0-or-later')
depends=('openssl' 'webkit2gtk-4.1')
makedepends=('git' 'cargo' 'npm' 'pkg-config')
source=("git+https://github.com/Athena-OS/mitre-attack-navigator.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $pkgname

  git submodule sync --recursive
  git submodule update --init --recursive
}

build() {
  cd $pkgname

  npm ci
  npm run tauri build -- --no-bundle
}

package() {
  cd $pkgname

  install -Dm 755 src-tauri/target/release/attack-navigator "$pkgdir/usr/bin/attack-navigator"
  install -Dm 644 attack-navigator.desktop "$pkgdir/usr/share/applications/attack-navigator.desktop"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" *.md
}