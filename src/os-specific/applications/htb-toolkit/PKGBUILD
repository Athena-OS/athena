pkgname=htb-toolkit
pkgver=56.7c95202
pkgrel=1
pkgdesc='Play Hack The Box machines directly on your system.'
arch=('x86_64' 'aarch64')
url='https://github.com/D3vil0p3r/htb-toolkit'
license=('GPL-3.0-or-later')
depends=('coreutils' 'gnome-keyring' 'gzip' 'imagemagick' 'libsecret'
         'noto-fonts-emoji' 'openssl' 'openvpn' 'ttf-nerd-fonts-symbols')
makedepends=('git' 'cargo')
source=("git+https://github.com/D3vil0p3r/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname
  
  cargo clean
  cargo build --release --locked
}

package() {
  cd $pkgname

  install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
