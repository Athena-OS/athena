pkgname=afterglow-recolored-green
_pkgname=Afterglow-Cursors-Recolored
pkgver=r11.g940a5d3
pkgrel=1
pkgdesc='Green recolors of the original Afterglow Cursors.'
arch=('any')
license=('GPL-3.0-or-later')
makedepends=('git')
source=("git+https://github.com/TeddyBearKilla/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  if desc=$(git describe --long --tags --abbrev=7 2>/dev/null); then
    count=$(echo "$desc" | awk -F- '{print $(NF-1)}')
    hash=$(echo "$desc"  | awk -F- '{print $NF}')
    printf "r%s.%s" "$count" "$hash"
  else
    printf "r%s.g%s" \
      "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  fi
}

package() {
  cd $_pkgname

  install -dm 755 "$pkgdir/usr/share/icons/Afterglow-Dracula-Green"
  cp -a colors/Dracula/Green/dist-dracula-green/* "$pkgdir/usr/share/icons/Afterglow-Dracula-Green/"
}

