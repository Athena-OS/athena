pkgname=afterglow-recolored-purple
_pkgname=Afterglow-Cursors-Recolored
pkgver=1.940a5d3
pkgrel=1
pkgdesc="Purple recolors of the original Afterglow Cursors."
arch=('any')
license=('GPL-3.0-or-later')
makedepends=('git')
source=("git+https://github.com/TeddyBearKilla/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $_pkgname
  
  install -dm 755 "$pkgdir/usr/share/icons/Afterglow-Recolored-Purple"
  cp -a colors/Original/Purple/dist-purple/* "$pkgdir/usr/share/icons/Afterglow-Recolored-Purple/"
}
