# Credits AUR ( https://aur.archlinux.org/ )
# Maintainer: Stephan Raabe <mail@ml4w.com>

pkgname=athena-hyprland-config
pkgver=2.11.0.r14.g20eed1c
pkgrel=1
pkgdesc='The ML4W Dotfiles for Hyprland - An advanced and full-featured configuration for the dynamic tiling window manager Hyprland including an easy to use installation script for Arch based Linux distributions.'
arch=('any')
url='https://github.com/mylinuxforwork/dotfiles'
license=('GPL')
options=(!strip)
depends=(
    'pacman-contrib'
    'wget'
    'zip'
    'unzip'
    'gum'
    'rsync'
    'git'
    'figlet'
    'stow'
    'sed'
    'vim'
    'xdg-user-dirs'
    'man-pages'
    'networkmanager'
    'bluez'
    'bluez-utils'
    'hyprland'
    'hyprpaper'
    'hyprshade'
    'hyprlock'
    'hypridle'
    'hyprpicker'
    'noto-fonts'
    'noto-fonts-extra'
    'xdg-desktop-portal-hyprland'
    'libnotify'
    'dunst'
    'kitty'
    'qt5-wayland'
    'qt6-wayland'
    'uwsm'
    'fastfetch'
    'xdg-desktop-portal-gtk'
    'eza'
    'python-pip'
    'python-gobject'
    'python-screeninfo'
    'tumbler'
    'brightnessctl'
    'nm-connection-editor'
    'network-manager-applet'
    'gtk4'
    'libadwaita'
    'fuse2'
    'imagemagick'
    'jq'
    'xclip'
    'firefox'
    'neovim'
    'htop'
    'nautilus'
    'nautilus-open-any-terminal'
    'python-pywal'
    'blueman'
    'grim'
    'slurp'
    'cliphist'
    'nwg-look'
    'qt6ct'
    'waybar'
    'rofi-wayland'
    'polkit-gnome'
    'zsh'
    'zsh-completions'
    'fzf'
    'pavucontrol'
    'papirus-icon-theme'
    'breeze'
    'flatpak'
    'swaync'
    'gvfs'
    'wlogout'
    'pinta'
    'gnome-text-editor'
    'gnome-calculator'
    'waypaper'
    'grimblast-git'
    'bibata-cursor-theme'
    'pacseek'
    'otf-font-awesome'
    'ttf-fira-sans'
    'ttf-fira-code'
    'ttf-firacode-nerd'
    'nwg-dock-hyprland'
    'oh-my-posh-bin'
)
conflicts=('ml4w-hyprland' 'ml4w-hyprland-git')
makedepends=('git')
source=("${pkgname}::git+https://github.com/mylinuxforwork/dotfiles.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  # share
  install -dm 755 "${pkgdir}/usr/share/ml4w-hyprland"
  cp -r "${pkgname}/share/." "${pkgdir}/usr/share/ml4w-hyprland"

   # lib
  install -dm 755 "${pkgdir}/usr/lib/ml4w-hyprland"
  cp -r "${pkgname}/lib/." "${pkgdir}/usr/lib/ml4w-hyprland"

  # bin
  install -Dm 755 "${pkgname}/bin/ml4w-hyprland-setup" "${pkgdir}/usr/bin/ml4w-hyprland-setup"

  # license
  install -Dm 755 "${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # doc
  install -Dm 755 "${pkgname}/README.md" "${pkgdir}/usr/share/doc/${pkgname}/README.md"

  # message
  echo
  echo ":: ML4W Dotfiles 2.9.8BETA2 for Hyprland - Rolling Release"
  echo ":: ------------------------------------------------------"
  echo ":: Please execute the command ml4w-hyprland-setup when the installation is complete."
  echo ":: You can start Hyprland already with command Hyprland or from your display manager."
  echo
}

