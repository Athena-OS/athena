pkgname=athena-lightdm-neon-theme
pkgver=0.2.4
pkgrel=1
pkgdesc='Modern and customizable theme for web-greeter with a nostalgic neon look'
arch=('any')
license=('GPL-1.0-or-later')
url='https://github.com/hertg/lightdm-neon'
depends=('lightdm' 'web-greeter')
makedepends=('npm' 'git')
optdepends=()
provides=()
conflicts=()
install='athena-lightdm-neon-theme.install'
source=("https://github.com/hertg/lightdm-neon/archive/refs/tags/v$pkgver.tar.gz"
        "lightdm.patch")
sha512sums=('8515dd8035631589aa2fdefcfa548b192d04ca1b2da4005d1842a77c825cd56ec0a4fc008d84946acc5f9749333526792bc74ac5f8c814be426f1746daa909bb'
            '8897853b2b28f54e2d5995c0f517a402eb370a8615a4e86006e861db06f70abaf024c82da22e9c0a1ca9c1206b9125c5e4e127b197448204070ad7a13a8be391')

prepare() {
    cd "lightdm-neon-${pkgver}"

    patch --forward --strip=1 --input="${srcdir}/lightdm.patch"
}

build() {
  cd "lightdm-neon-${pkgver}"

  sed -i "s/hello neon/Welcome to Athena OS/g" ./src/store/settings.ts
  sed -i "s/SignFont.Neonderthaw/SignFont.Neoneon/g" ./src/store/settings.ts
  npm install
  npm run build
}

package() {
  # note: the directory is called "lightdm-neon-{version}", because the github 
  # archive extracts to a directory called "{repository}-{version}", not what
  # is specified in the source() of the PKGBUILD

  cd "lightdm-neon-${pkgver}"

  install -dm755 "${pkgdir}/usr/share/web-greeter/themes/neon"
  cp -r public/. "${pkgdir}/usr/share/web-greeter/themes/neon"
}

