pkgname=athena-lightdm-neon-theme
pkgver=0.2.3
pkgrel=1
pkgdesc='Modern and customizable theme for web-greeter with a nostalgic neon look'
arch=('any')
license=('GPL-1.0-or-later')
url='https://github.com/hertg/lightdm-neon'
depends=('lightdm' 'web-greeter')
makedepends=('npm' 'git')
optdepends=()
provides=()
conflicts=()
install='athena-lightdm-neon-theme.install'
source=("${pkgname}-0.2.3.tar.gz::https://github.com/hertg/lightdm-neon/archive/refs/tags/v0.2.3.tar.gz"
        "lightdm.patch")
sha512sums=('99b3c1cfca997408e843141adc0c7597aebd3c618f88a989ff9cf98e77c674eab00c9ed7bc8b94c744deed28244ed560c3b11b776e09a94985b515e3ea6b8cf9'
            '8897853b2b28f54e2d5995c0f517a402eb370a8615a4e86006e861db06f70abaf024c82da22e9c0a1ca9c1206b9125c5e4e127b197448204070ad7a13a8be391')

prepare() {
    cd lightdm-neon-${pkgver}
    patch --forward --strip=1 --input="${srcdir}/lightdm.patch"
}

build() {
  cd lightdm-neon-${pkgver}
  sed -i "s/hello neon/Welcome to Athena OS/g" ./src/store/settings.ts
  sed -i "s/SignFont.Neonderthaw/SignFont.Neoneon/g" ./src/store/settings.ts
  npm install
  npm run build
}

package() {
  # note: the directory is called "lightdm-neon-{version}", because the github 
  # archive extracts to a directory called "{repository}-{version}", not what
  # is specified in the source() of the PKGBUILD

  cd "${srcdir}/lightdm-neon-${pkgver}"
  install -dm755 "${pkgdir}/usr/share/web-greeter/themes/neon"
  cp -r public/. "${pkgdir}/usr/share/web-greeter/themes/neon"
}
