# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=grig
pkgver=0.8.1
pkgrel=1
pkgdesc='Rig control (cat) program.'
arch=('x86_64' 'aarch64')
url='https://groundstation.sourceforge.net/grig/'
license=('GPL-1.0-or-later')
depends=('hamlib' 'gtk2' 'intltool')
source=("https://downloads.sourceforge.net/groundstation/$pkgname-$pkgver.tar.gz")
sha512sums=('84eda8db5d18324f26f0775483c8272296aad05dd75f0572c01a3c7af6dbd14bff7b98cc6a61fe3c43d25193866be4b3dafdc4e68dfc37217e88b53ca53266f9')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i -e "s:GtkTooltips:GtkTooltip:" src/grig-gtk-workarounds.c
	sed -i -e "s:GtkTooltips:GtkTooltip:" src/rig-gui-vfo.c
	sed -i -e "s:GtkTooltips:GtkTooltip:" src/rig-gui-buttons.c
	sed -i -e "s:GtkTooltips:GtkTooltip:" src/rig-gui-levels.c

  autoreconf -i
}

build() {
	cd "$pkgname-$pkgver"

  if [[ "$CARCH" == "i686" || "$CARCH" == "x86_64" ]];
  then
    ./configure --prefix=/usr
  else
    ./configure --prefix=/usr --build=arm
  fi

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
