# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-safedexml
_pkgname=${pkgname#python-}
pkgver=0.5.1
pkgrel=7
pkgdesc='A defusedxml version of dead-simple Object-XML mapper for Python.'
arch=('any')
url='https://pypi.org/project/safedexml/#files'
license=('MIT')
depends=('python' 'python-defusedxml')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('3e9ebef0044d3319aae98a71cdb9790639c8eaadd9bd0775aa95c4bf56daaa1bd79614ee1600a347f757365ef447ce973e0b4e596a3d500b9c148a94efb0ee0a')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

