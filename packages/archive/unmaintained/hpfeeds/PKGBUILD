# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=hpfeeds
pkgver=414.34bf4d4
_pyver=3.13
pkgrel=2
pkgdesc='Honeynet Project generic authenticated datafeed protocol.'
arch=('any')
groups=('role-redteamer' 'role-network' 'athena' 'athena-honeypot'
        'athena-networking')
url='https://github.com/rep/hpfeeds'
license=('BSD')
depends=('python' 'python-aiohttp' 'python-attrs' 'python-async-timeout'
         'python-chardet' 'python-idna' 'python-multidict' 'python-yarl'
         'python-prometheus_client' 'python-wrapt' 'python-aiorun'
         'python-idna_ssl')
makedepends=('git' 'python-setuptools')
replaces=('python-hpfeeds3')
provides=('python-hpfeeds3' 'hpfeeds')
source=("git+https://github.com/rep/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"
}

