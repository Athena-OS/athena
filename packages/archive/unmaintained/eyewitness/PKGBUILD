# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=eyewitness
pkgver=v20230525.1.r137.g8a21526
pkgrel=1
groups=('role-bountyhunter' 'role-webpentester' 'role-redteamer' 'role-osint'
        'athena' 'athena-webapp' 'athena-recon' 'athena-misc')
pkgdesc='Designed to take screenshots of websites, provide some server header info, and identify default credentials if possible.'
arch=('any')
url='https://github.com/RedSiege/EyeWitness'
license=('GPL-3.0-or-later')
depends=('python' 'python-pyqt4' 'python-netaddr' 'python-rdpy'
         'python-selenium' 'python-rsa' 'xorg-server-xvfb'
         'python-beautifulsoup4' 'python-pyvirtualdisplay'
         'python-fuzzywuzzy' 'python-pillow' 'geckodriver'
         'python-easyprocess' 'python-pytesseract'
         'python-levenshtein')
makedepends=('git')
source=("$pkgname::git+https://github.com/RedSiege/EyeWitness.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $pkgname

  grep -iRl 'python' * | xargs sed -i 's|python$|python|'
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md CHANGELOG
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md CHANGELOG LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec python /usr/share/$pkgname/Python/EyeWitness.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

