# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=fbi
pkgver=6.e9ef3e0
pkgrel=1
pkgdesc='An accurate facebook account information gathering.'
groups=('athena' 'athena-social' 'athena-recon')
arch=('any')
url='https://github.com/KnightSec-Official/fbi'
license=('GPL-2.0-or-later')
depends=('python2' 'python2-requests')
makedepends=('git')
source=("git+https://github.com/KnightSec-Official/$pkgname.git"
        "$pkgname.py.patch")
sha512sums=('SKIP'
            'c4d08fa05d79b5bee874ec9f4b6d1f2dbd6ca221070c9b4f17661916f52675fd50d2996aa518adb852f83744a68471b3b97cc47dee8822ff07f91092da310731')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  patch -p1 < "$pkgname.py.patch"
}

package() {
  cd $pkgname

  install -Dm 755 "$pkgname.py" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

