# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=visql
pkgver=22.77997a5
pkgrel=1
pkgdesc='Scan SQL vulnerability on target site and sites of on server.'
groups=('role-webpentester' 'role-redteamer' 'athena' 'athena-scanner'
        'athena-webapp')
arch=('any')
url='https://github.com/ethicalhackeragnidhra/viSQL'
license=('MIT')
depends=('python2' 'python2-requests' 'python2-colorama')
makedepends=('git')
source=("$pkgname::git+https://github.com/ethicalhackeragnidhra/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm README.md LICENSE

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 viSQL.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}
