# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=depdep
pkgver=16.5e556e9
pkgrel=1
groups=('athena' 'athena-networking' 'athena-malware')
pkgdesc='A merciless sentinel which will seek sensitive files containing critical info leaking through your network.'
arch=('any')
url='https://github.com/bedirhan/depdep'
license=('custom:unknown')
depends=('python2' 'smbclient' 'cifs-utils' 'java-environment' 'apache-tika')
source=("git+https://github.com/bedirhan/$pkgname.git"
        'https://raw.githubusercontent.com/galkan/depdep/master/LICENSE.txt')
sha512sums=('SKIP'
            '38f659b5c3eea636e8ed819fa9ebf10b5590c469ca5ab8bf0a3462d57d438fa2f65647a70f21b6b00531f82f82d228d50271d23325d780dd3a525edeaf5b39d3')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare(){
  cd $pkgname

  sed -i 's|python$|env python2|' *.py
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"

  for i in config lib script ; do
    install -dm 755 "$pkgdir/usr/share/$pkgname/$i"
    cp -a --no-preserve=ownership $i/* "$pkgdir/usr/share/$pkgname/$i/"
  done

  install -Dm 755 "$pkgname.py" "$pkgdir/usr/share/$pkgname/$pkgname.py"
  install -Dm 644 $srcdir/LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

