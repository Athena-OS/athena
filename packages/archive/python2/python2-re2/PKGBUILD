# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-re2
_pkgname=${pkgname#python2-}
pkgver=0.2.24
pkgrel=5
pkgdesc="Python wrapper for Google's RE2 using Cython."
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/re2/#files'
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools' 're2')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz"
        "fix-build.patch")
sha512sums=('b056cfbf4d227afb3fc83fa8ecbc16311a009d5c7132fcdf1eba827d76c1c3ef90545dbfa97cabec22a50b85720f7af9284e9317302adc130e1205ff1b78f0ea'
            '398ff9f619388a09701df16be105287393c475c7cbcc8a5b6c6364618885ea848815b3f5b668c35a0c2bdba639cb5f48c3d5514579a06e3ee6b7fb4aecc338c1')

prepare() {
  cd "$_pkgname-$pkgver"

  patch -p1 < ../fix-build.patch
}

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}
