# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=cython2
_pkgname=Cython
pkgver=0.29.37
pkgrel=1
pkgdesc='The Cython compiler for writing C extensions for the Python language.'
url='https://pypi.org/project/Cython/#files'
arch=('x86_64' 'aarch64')
license=('Apache-2.0')
depends=('python2' 'python2-setuptools')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('556b0bdcc40f90fa35053ad319d44cd1e71686a6dc285dc4b36399851201834309f3681b625aaf905cc3ffd4a6b9ec8aa8a29511ff52e79303b629a27243c254')

prepare() {
  cd "$_pkgname-$pkgver"

  find . -name '*.py' |
  xargs sed -i -e 's|/usr/bin/env python|/usr/bin/env python2|' \
  -e 's|/usr/bin/python|/usr/bin/python2|'
}

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  for i in "$pkgdir/usr/bin/"*; do
    mv $i "${i}2"
  done
}

