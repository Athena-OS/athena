# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=spike-fuzzer
pkgver=5.2e09f8e
pkgrel=1
groups=('athena' 'athena-fuzzer')
pkgdesc="IMMUNITYsec's fuzzer creation kit in C."
arch=('x86_64' 'aarch64')
url='https://github.com/guilhermeferreira/spikepp'
license=('GPL-2.0-or-later')
depends=('gcc' 'automake' 'openssl' 'autoconf')
optdepends=('python2: needed to run SPIKE_Console.py python GUI'
            'wxpython: needed to run SPIKE_Console.py python GUI')
source=("git+https://github.com/guilhermeferreira/spikepp.git")
sha512sums=('SKIP')

pkgver() {
  cd spikepp

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd spikepp/SPIKE/src

  sed -i 's|/usr/bin/python|/usr/bin/python2|g' SPIKE_Console.py
}

build() {
  cd spikepp/SPIKE/src

  #aclocal
  #automake --add-missing
  #autoconf

  autoreconf -f -i
  ./configure --prefix=/usr

  make
}

package() {
  cd spikepp/SPIKE/src

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/lib"
  install -dm 755 "$pkgdir/usr/include/$pkgname"
  install -dm 755 "$pkgdir/usr/share/$pkgname"
  install -dm 755 "$pkgdir/usr/share/doc/$pkgname"

  # Install binaries.
  # The package doesn't provide a decent method of installation.
  for bin in citrix closed_source_web_server_fuzz dceoversmb dltest do_post \
    generic_chunked generic_listen_tcp generic_send_tcp generic_send_udp \
    generic_web_server_fuzz generic_web_server_fuzz2 gopherd halflife \
    line_send_tcp msrpcfuzz msrpcfuzz_udp ntlm2 ntlm_brute pmspike post_fuzz \
    post_spike quake quakeserver sendmsrpc ss_spike statd_spike sunrpcfuzz \
    webfuzz x11_spike ; do
    install -m 755 "$bin" "$pkgdir/usr/bin/$pkgname-${bin/_/-}"
  done

  install -m 755 *.so "$pkgdir/usr/lib/"

  cd ..

  install -m 644 include/* "$pkgdir/usr/include/$pkgname"
  install -m 644 *.txt documentation/* \
          "$pkgdir/usr/share/doc/$pkgname"

  cp --no-preserve=ownership -R dcedump data encrypted \
    "$pkgdir/usr/share/$pkgname/"
}

