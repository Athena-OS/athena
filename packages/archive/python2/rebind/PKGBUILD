# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=rebind
pkgver=0.3.4
pkgrel=6
groups=('role-redteamer' 'athena' 'athena-exploitation')
pkgdesc='DNS Rebinding Tool.'
arch=('x86_64' 'aarch64')
url='https://code.google.com/p/rebind/'
license=('MIT')
depends=('python2' 'sqlite3' 'readline')
makedepends=('subversion')
source=("https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/rebind/${pkgname}_${pkgver//./-}.tar.gz")
sha512sums=('1d220f7815598d972442bb2bab3c5d699443b339e5b77d9b71b279beca8b6413909f55f635e802642bce1f954852f3a2c7febe5b01ad3112065360af5e8b23ec')

prepare(){
  cd "$pkgname/dev"

  sed -i 's/env python/env python2/' *.py
  sed -i 's/CFLAGS=/CFLAGS=-fcommon /' ../src/Makefile

  chmod +x *.py
}

build(){
  cd "$pkgname/src"

  make
}

package(){
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"
  install -dm 755 "$pkgdir/usr/share/doc/$pkgname"

  rm -rf bin/rebind_{i686,x86-64}

  cp -a --no-preserve=ownership src/www "$pkgdir/usr/share/doc/$pkgname/"
  cp -a --no-preserve=ownership docs/* "$pkgdir/usr/share/doc/$pkgname/"

  # Renamed to fix conflict with community/elfkickers.
  install -m 755 src/rebind "$pkgdir/usr/bin/rebind_dns"
}

