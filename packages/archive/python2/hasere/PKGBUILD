# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=hasere
pkgver=24.882399b
pkgrel=1
groups=('role-redteamer' 'role-osint' 'athena' 'athena-recon' 'athena-scanner')
pkgdesc='Discover the vhosts using google and bing.'
arch=('any')
url='https://github.com/hotelzululima/hasere'
license=('custom:unknown')
depends=('python2')
optdepends=('nmap: Adds nmap support')
source=("git+https://github.com/hotelzululima/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|python$|env python2|' *.py
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname/"{data,lib}

  install -Dm 755 "$pkgname.py" "$pkgdir/usr/share/$pkgname"

  cp -a --no-preserve=ownership data/* "$pkgdir/usr/share/$pkgname/data/"
  cp -a --no-preserve=ownership lib/* "$pkgdir/usr/share/$pkgname/lib/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python2 $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}
