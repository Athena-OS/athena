# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=goog-mail
pkgver=32.544ee4d
pkgrel=1
pkgdesc='Enumerate domain emails from google.'
url='https://github.com/BushidoUK/Goog-mail'
groups=('role-redteamer' 'role-osint' 'athena' 'athena-recon')
arch=('any')
license=('custom:unknown')
depends=('python2')
source=("$pkgname::git+https://github.com/BushidoUK/Goog-mail.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|/usr/bin/python|/usr/bin/env python2|' $pkgname.py
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
exec python2 /usr/share/$pkgname/$pkgname.py "\$@"
EOF

  chmod a+x "$pkgdir/usr/bin/$pkgname"
}

