# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sslmap
pkgver=5.63e10a0
pkgrel=1
groups=('athena' 'athena-scanner' 'athena-crypto')
pkgdesc='A lightweight TLS/SSL cipher suite scanner.'
arch=('any')
url='https://github.com/iphelix/sslmap'
license=('unknown')
depends=('python2')
source=("git+https://github.com/iphelix/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' "$pkgname.py"
}

package() {
  cd $pkgname

  install -Dm 755 "$pkgname.py" "$pkgdir/usr/bin/$pkgname"
}

