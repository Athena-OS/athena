# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=sparty
pkgver=6.777fd94
pkgrel=1
groups=('role-webpentester' 'role-redteamer' 'athena' 'athena-webapp')
pkgdesc='An open source tool written in python to audit web applications using sharepoint and frontpage architecture.'
arch=('any')
url='https://github.com/adityaks/sparty'
license=('BSD')
depends=('python2')
source=("git+https://github.com/adityaks/$pkgname")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  sed -i 's|/usr/bin/python|/usr/bin/python2|' "sparty_v_0.1/sparty_v_0.1.py"
  sed -i 's|(2,6):|(2,7):|' "sparty_v_0.1/sparty_v_0.1.py"
}

package() {
  cd $pkgname

  install -Dm 755 "sparty_v_0.1/${pkgname}_v_0.1.py" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 sparty_v_0.1/README "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm 644 sparty_v_0.1/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

