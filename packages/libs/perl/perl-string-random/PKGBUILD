# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=perl-string-random
_pkgname=String-Random
pkgver=0.32
pkgrel=1
pkgdesc='Perl module to generate random strings based on a pattern.'
arch=('any')
url='https://metacpan.org/pod/String::Random'
license=('PerlArtistic' 'GPL-1.0-or-later')
depends=('perl')
makedepends=('perl-module-build')
options=(!emptydirs)
source=("https://cpan.metacpan.org/authors/id/S/SH/SHLOMIF/$_pkgname-$pkgver.tar.gz")
sha512sums=('1dd0e7e45e03e155b7a0d8394fb3a04324f4827fe9431e9697d07c3a0f3941981e0b3de1e9f843a43936e5423146a3e6d4da075da04297525093ceffc3b1e659')

build() {
  cd "$_pkgname-$pkgver"

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  /usr/bin/perl Build.PL INSTALLDIRS=vendor
  ./Build
}

package() {
  cd "$_pkgname-$pkgver"

  ./Build install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

