# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=perl-net-rawip
_pkgname=Net-RawIP
pkgver=0.25
pkgrel=15
pkgdesc='Perl extension to manipulate raw IP packets with interface to B<libpcap>.'
arch=('x86_64' 'aarch64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('libpcap' 'perl')
url='http://search.cpan.org/dist/Net-RawIP'
source=("http://search.cpan.org/CPAN/authors/id/S/SA/SAPER/$_pkgname-$pkgver.tar.gz"
        'fixes.patch')
sha512sums=('c48c77389db9dc8d5c64d67e5c62bac5e3314dbc36bcfa51aa20d07224492edaa65f5c71d47ca0b804ac62b1b02387e172cd3561eadaa3a1a8040968b182b3ca'
            '615d6facf077e3cded746709034a49d0a69426d6868ade15708f795e2b9252e6269c0a5fe7578b57ae723e9e570b8f55fcbf0306ee99eb925aa74ffff8c4a255')

prepare() {
  cd "$_pkgname-$pkgver"

  patch -Np2 -b -z .orig <../fixes.patch
}

build() {
  cd "$_pkgname-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd "$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  find "$pkgdir" -name '.packlist' -o -name '*.pod' -delete
}

