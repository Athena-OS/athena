# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-mispy
_pkgname=${pkgname#python-}
pkgver=0.5
pkgrel=4
pkgdesc='A pythonic MISP module.'
arch=('any')
url='https://pypi.org/project/mispy/#files'
license=('Apache-2.0')
depends=('python' 'python-lxml' 'python-requests')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('cf9ce53b7b2596e2f5baf0c35ea6a7d478afef3e30025c07f2c5ab0657cfadaec2cd915f243ae64ae60a2374e962ed61d7393944a2e7f731894ea5f2acda43b8')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

