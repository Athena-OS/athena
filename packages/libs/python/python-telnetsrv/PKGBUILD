# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-telnetsrv
_pkgname=${pkgname#python-}
pkgver=0.4
pkgrel=8
pkgdesc='Telnet server handler library.'
arch=('any')
url='https://pypi.org/project/telnetsrv/#files'
license=('GPL-3.0-or-later')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('75c69378fecc5f344560d05f2195d9832add03c53a2955b95b030e2f72da3351cc9712fedd08ae576f2e175f8a887f3bc3f67155760fdc2749cb270cef5d4fba')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

