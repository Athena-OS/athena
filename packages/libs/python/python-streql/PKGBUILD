# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-streql
_pkgname=${pkgname#python-}
pkgver=3.0.2
pkgrel=4
pkgdesc='Constant-time string comparison.'
arch=('any')
url='https://pypi.org/project/streql/#files'
license=('Apache-2.0')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('8bde506923f340ee454695a0d42bc262481c321082b914476a025d7276ed2402c504a10f62f765a32b3746f2e2be068ba76876803ead2819db0153f7524c41cd')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

