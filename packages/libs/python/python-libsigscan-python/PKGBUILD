# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-libsigscan-python
_pkgname=${pkgname#python-}
pkgver=20240505
pkgrel=1
pkgdesc='Python bindings module for libsigscan.'
arch=('x86_64' 'aarch64')
url='https://pypi.python.org/pypi/libsigscan-python/'
license=('LGPL-3.0-or-later')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('8525ca2a0c23475f1e9af70844959f73024fd74a68fdb1a531ac0603c1f1d527f2691a420edc75074a3939a2db22a2e5e3f4d636f9826c01593e80067df1522b')

build() {
  cd "libsigscan-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "libsigscan-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

