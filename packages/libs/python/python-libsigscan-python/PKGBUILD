# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libsigscan-python
_pkgbase=${pkgbase#python-}
pkgname=('python2-libsigscan-python' 'python-libsigscan-python')
pkgver=20230109
pkgrel=3
pkgdesc='Python bindings module for libsigscan.'
arch=('x86_64' 'aarch64')
url='https://pypi.python.org/pypi/libsigscan-python/'
license=('LGPL-3.0-or-later')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgbase::1}/$_pkgbase/$_pkgbase-$pkgver.tar.gz")
sha512sums=('0f311cc4ede717e22d6f71c39f65954a46e2a48e80c567a985d930a1336e1c353f66252625ad3a97b61211242c4017a37aa70ded3513f494b635e05fa74075d3')

prepare() {
  cp -a "libsigscan-$pkgver"{,-2}
}

build() {
  cd "libsigscan-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/libsigscan-$pkgver"

  python setup.py build
}

package_python2-libsigscan-python() {
  depends=('python2')

  cd "libsigscan-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-libsigscan-python() {
  depends=('python')

  cd "libsigscan-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

