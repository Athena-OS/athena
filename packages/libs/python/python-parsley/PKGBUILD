# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-parsley
_pkgname=Parsley
pkgver=1.3
pkgrel=8
pkgdesc='Parsing and pattern matching made easy.'
arch=('any')
url='https://pypi.python.org/pypi/parsley/'
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://pypi.python.org/packages/06/52/cac2f9e78c26cff8bb518bdb4f2b5a0c7058dec7a62087ed48fe87478ef0/$_pkgname-$pkgver.tar.gz")
sha512sums=('b87ebc44796e8511ad113f06d7bd4ebfec0161cfc8cd4d1db214da821ff61884328931ab39fdf34d33df5c9e803a1c3f5ac31e7ede176262f54b8b0a9cf3818b')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

