# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pgcli
_pkgname=${pkgname#python-}
pkgver=4.1.0
pkgrel=1
pkgdesc='CLI for Postgres Database. With auto-completion and syntax highlighting.'
arch=('any')
url='https://pypi.org/project/pgcli/#files'
license=('BSD')
depends=('python' 'python-click' 'python-pygments' 'python-psycopg2'
         'python-sqlparse' 'python-configobj' 'python-pgspecial'
         'python-setproctitle' 'python-cli-helpers' 'python-humanize')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('aa2cf018a9fd17ca1178984a84170a0a3eefff84901779be6ea5ce2b124a99ee7bfd0e70987b4b1476c5a7c67008e8fe55b3053a85065291407825ef3e273bf7')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

