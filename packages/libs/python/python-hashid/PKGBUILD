# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-hashid
_pkgname=hashID
pkgver=397.7e8473a
pkgrel=1
groups=('athena' 'athena-crypto')
pkgdesc='Software to identify the different types of hashes.'
arch=('any')
url='https://github.com/psypanda/hashID'
license=('GPL-3.0-or-later')
depends=('python')
makedepends=('python-setuptools')
options=(!emptydirs)
source=("git+https://github.com/psypanda/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $_pkgname

  python setup.py build
}

package() {
  cd $_pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

