# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-ipcalc
_pkgname=${pkgname#python-}
pkgver=1.99.0
pkgrel=8
epoch=1
pkgdesc='IP subnet calculator for Python.'
arch=('any')
url='https://pypi.org/project/ipcalc/#files'
license=('custom:unknown')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('14317b10775e7169633419ff46caf8da693efb0b46e3baced31adf1e7ed389cba9f8c265cc515b20062bf992bcf8e931fffe12d6656dad11585444d6e445b224')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

