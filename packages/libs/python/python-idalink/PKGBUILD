# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-idalink
_pkgname=${pkgname#python-}
pkgver=0.12
pkgrel=8
pkgdesc='An interface to the insides of IDA.'
arch=('any')
url='https://pypi.org/project/idalink/#files'
license=('GPL-3.0-or-later')
depends=('python' 'python-rpyc')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('2b65e7563100c1adbd21642a35ea53f8630075aa025f2fc7999c636922d12ee1def0761eb269f97dc1ac747deb8bc8b186ba2c067d4c613dcbc83287dafa478b')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

