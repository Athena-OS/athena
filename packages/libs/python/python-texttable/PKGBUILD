# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-texttable
_pkgname=${pkgname#python-}
pkgver=1.7.0
pkgrel=1
pkgdesc='Generate a formatted text table using ASCII characters.'
arch=('any')
url="https://pypi.python.org/pypi/texttable/"
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
optdepends=('python-cjkwrap: for better CJK support')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('427ab262301bc38066c4a54e8ef10163579b5d6210aeb003a729dbaef8badd347a2d01f9ea1d9d136611fc104a3a8cf59a7a9acac6f76f64f9575dc6e5ab4313')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

