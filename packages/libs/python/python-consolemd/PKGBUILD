# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-consolemd
_pkgname=${pkgname#python-}
pkgver=0.5.1
pkgrel=5
pkgdesc='ConsoleMD renders markdown to the console.'
arch=('any')
url='https://pypi.org/project/consolemd/#files'
license=('MIT')
depends=('python' 'python-pygments' 'python-six' 'python-setproctitle'
         'python-commonmark')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('5eae413b3aa69b1f2d76e6bfca14a3742bab0a5e2b1f37bffa2490d1be02dda73b39fcc86fc70db4d3d737ec64ac018bdde69f19bdf38d15bea05390b8659133')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

