# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-boofuzz
_pkgname=${pkgname#python-}
pkgver=0.4.2
pkgrel=1
pkgdesc='Boofuzz is a fork of Sulley fuzzing framework and aims for extensibility. The goal: fuzz everything.'
arch=('any')
url='https://pypi.org/project/boofuzz/'
license=('GPL-2.0-or-later')
depends=( 'python' 'python-attrs' 'python-backports.shutil-get-terminal-size'
          'python-click' 'python-colorama' 'python-flask' 'python-funcy'
          'python-future' 'impacket' 'python-psutil' 'python-pyserial'
          'python-pydot' 'python-six' 'python-tornado')
makedepends=('python-build' 'python-pip')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('d872d5939dbf63c6500aca06d64e32cf321ef66aae770643a3a93ccf1c75e0931cb1521b13431f2ca1d874922fb0bc061ef45978df3d4100d07eaf019f1a7041')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}
