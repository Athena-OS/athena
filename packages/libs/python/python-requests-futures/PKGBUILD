# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-requests-futures
_pkgname=requests_futures
pkgver=1.0.2
pkgrel=1
pkgdesc='Asynchronous Python HTTP for Humans.'
arch=('any')
url='https://pypi.org/project/requests-futures/#files'
license=('Apache-2.0')
depends=('python' 'python-certifi' 'python-charset-normalizer' 'python-idna' 'python-requests' 'python-urllib3')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('1d163ffe45a09e0160710cf8bd060f741a524e60d69318af7c03a3b5dc41f27f4f7d115faed227bb710b29ffdb230a7ef5991e33ab2ad4e25b3c52727ef76fc7')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

