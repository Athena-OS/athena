# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-libkeepass
_pkgname=${pkgname#python-}
pkgver=0.3.1.post1
pkgrel=6
pkgdesc='A library to access KeePass 1.x/KeePassX (v3) and KeePass 2.x (v4) files.'
arch=('any')
url='https://pypi.org/project/libkeepass/'
license=('GPL-1.0-or-later')
depends=('python' 'python-lxml' 'python-pycryptodome' 'python-colorama')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('b9fa0c87ea33a9bcd7c5e138c1eff3809e46a196970580960a725afe5b51db124ad9aa26d3e7b462bd5de5d5e6e679edc6e3301b9702a95678365bec271d533f')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

