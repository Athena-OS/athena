# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pythonaes
_pkgname=${pkgname#python-}
pkgver=1.0
pkgrel=5
pkgdesc='Pure Python implementation of AES for 2.7 or 3.x.'
url='https://pypi.org/project/pythonaes/#files'
arch=('any')
license=('custom:unknown')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('1e812307c9213f0a44bed738d5fd592adcbd26507da87312955aebffa0a4e7adba504cb73707b95d2d357927f10617d93af6383ab829a95156fdc19719e9dd3c')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

