# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pcapfile
_pkgname=pypcapfile
pkgver=0.12.0
pkgrel=8
pkgdesc='Pure Python package for reading and parsing libpcap savefiles.'
arch=('any')
url='https://pypi.python.org/pypi/pypcapfile/'
license=('ISC')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://pypi.python.org/packages/5c/a8/70677db49ecd688c7207e525664864d709f4aa2f76c29964cc93825f34b8/$_pkgname-$pkgver.tar.gz")
sha512sums=('6c2c238f2732b4d5bc5f2b4c7b9e66359d964b94ead437c790256f9bef4e27ed10abaa3e4f17cd1bec22ce0d56cab6e2a63b784a49087943911f1dccd63c1127')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

