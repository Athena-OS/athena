# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-xhtml2pdf
_pkgname=${pkgname#python-}
pkgver=0.2.16
pkgrel=1
pkgdesc='PDF generator using HTML and CSS.'
arch=('any')
url='https://pypi.org/project/xhtml2pdf/#files'
license=('Apache-2.0')
depends=('python' 'python-html5lib' 'python-pillow' 'python-six'
         'python-pypdf2' 'python-reportlab')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('aa7cf478338dbf1430c8cb08100964557f944a3ced22f5b755a4d6cd4d8cd94852db92a13995b3647a6ca3c5c5f41b9ebabcfdb82664fe8fde6ad8a29d40d90a')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

