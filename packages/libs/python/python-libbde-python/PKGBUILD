# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-libbde-python
_pkgname=${pkgname#python-}
pkgver=20240502
pkgrel=1
pkgdesc='Library and tools to access the BitLocker Drive Encryption (BDE) encrypted volumes.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/libbde-python/#files'
license=('LGPL-3.0-or-later')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('4708554a3a594428005b568907cedea4b0491257c5e33e26053ab50615da43bc49f4df9cb753f488f9fd3d9aabef1a3fab43c0c4116d48627a6995d477e1a4a9')

build() {
  cd "libbde-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "libbde-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

