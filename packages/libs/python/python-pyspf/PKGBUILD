# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyspf
_pkgname=${pkgname#python-}
pkgver=2.0.14
pkgrel=3
pkgdesc='SPF (Sender Policy Framework) implemented in Python.'
arch=('any')
url='https://pypi.org/project/pyspf/'
license=('PSF')
depends=('python' 'python-dnspython')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/p/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('ca801a899a80c18cd2c6bb8c125817c29898189805040cce8d29ace7a5aa34b43b9d10545150a1f39abd7bc49cd8da4fe753a04ac9b911e7bb9d77473753eeee')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  for s in "$pkgdir/usr/bin/"*; do
    arrS=(${s//./ })
    mv $s "${arrS[0]}"
  done
}

