# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-win_inet_pton
_pkgname=${pkgname#python-}
pkgver=1.1.0
pkgrel=6
pkgdesc='Native inet_pton and inet_ntop implementation for Python on Windows (with ctypes).'
arch=('any')
url='https://pypi.org/project/win_inet_pton/#files'
license=('custom:unknown')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('7ee317d6861e6ea15db3269e0eb107dd32edc912c3011b38bd8c95f3ca7efdf96954ea7395cc20469352bf06445a662a7c90954df33cb61cc47af8a1fd40bae5')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

