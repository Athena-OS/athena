# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-oletools
_pkgname=${pkgname#python-}
pkgver=0.60.2
pkgrel=1
epoch=1
groups=('role-malware' 'role-forensic' 'athena' 'athena-binary'
        'athena-forensic')
pkgdesc='Tools to analyze Microsoft OLE2 files.'
arch=('any')
url='https://pypi.org/project/oletools/'
license=('GPL-2.0-or-later')
depends=('python' 'python-colorclass' 'python-pyparsing' 'python-olefile'
         'python-easygui' 'python-msoffcrypto-tool')
makedepends=('python-setuptools')
source=("https://github.com/decalage2/oletools/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('d555d329c7949ed2635813975536397b69e0f7c7e2846587bf844796c4cc988eab95aeb51abd672fdf2eb270329cd6c69ced3f8c41a28f0d8e8dc2be13e6908f')

prepare() {
  cd "$_pkgname-$pkgver"

  sed -i 's|pyparsing>=2.1.0,<3|pyparsing|g' setup.py
  sed -i 's|pyparsing>=2.1.0,<3|pyparsing|g' requirements.txt
}

build() {
  cd "$_pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/doc/$_pkgname/" README.md
  install -Dm 644 oletools/LICENSE.txt \
    "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"

  cp oletools/doc/* "$pkgdir/usr/share/doc/$_pkgname"
}

