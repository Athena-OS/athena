# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-getmac
_pkgname=${pkgname#python-}
pkgver=0.9.5
pkgrel=1
pkgdesc='Get MAC addresses of remote hosts and local interfaces.'
arch=('any')
url='https://pypi.org/project/getmac/#files'
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('d3ab0fdf6a346949802df2be9b908daf4234fcf375bd2570d2f1440d326ed6314337685c3a0788ba943c337f6c110838db383ae7283df3f2ac288b70a44b9454')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

