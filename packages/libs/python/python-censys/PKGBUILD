# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-censys
_pkgname=${pkgname#python-}
pkgver=2.2.16
pkgrel=1
epoch=1
pkgdesc='Python library for interacting with Censys Search Engine (censys.io).'
arch=('any')
url='https://pypi.org/project/censys/#files'
license=('Apache-2.0')
depends=('python' 'python-requests' 'python-netaddr' 'python-backoff'
         'python-rich')
makedepends=('python-build' 'python-pip')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('0d1481a6ed7dd7e8f72af635f41bcfa4c3a2c7c4c7534bca9cb77cfad29c8f8ccf33f114f662c69436972a20cb5a0a1990e23b59cef45dd4a07bbf4cc9517063')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}

