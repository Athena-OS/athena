# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-passivetotal
_pkgname=${pkgname#python-}
pkgver=2.5.9
_pyver=3.13
pkgrel=5
pkgdesc='Client for the PassiveTotal REST API.'
arch=('any')
url='https://pypi.org/project/passivetotal/#files'
license=('GPL-2.0-or-later')
depends=('python' 'python-dateutil' 'python-future' 'python-requests')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('cb7968a6910702aa4e073d4f1bf5405f6ff6d9cbc9dae95d9ed04b853ce34e72099b9752b9f09d640602e6169815db3198c411bc7ab7ee56f933f2dd366ff8ed')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests/"
}

