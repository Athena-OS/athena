# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-guess-language
_pkgname=${pkgname#python-}
pkgver=0.2
pkgrel=9
pkgdesc='Attempts to determine the natural language of a selection of Unicode (utf-8) text.'
arch=('any')
url='https://pypi.python.org/pypi/guess-language'
license=('LGPL')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('0badf3566d01ddac1c0da10cfd649515ed2b2e9b059fce32114a7dbcac91c0aa12c6da803181c21e6cc745bb8725885da1f2f4ee0738bc5230d6d5e567eecb10')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

