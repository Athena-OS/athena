# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pygraphviz
_pkgname=${pkgname#python-}
pkgver=1.14
pkgrel=1
epoch=1
pkgdesc='Python interface to the Graphviz graph layout and visualization package.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pygraphviz/#files'
license=('LGPL')
depends=('python' 'graphviz')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('867346303d560be21a37080e6d3ce0afe42119ee373f8cd872e853fee611d2c4cb56cc057023230fcf8ffbf03053ea92b9fc2ec6d5e682d6c6667b6727d87899')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

