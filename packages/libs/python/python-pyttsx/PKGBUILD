# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyttsx
_pkgname=${pkgname#python-}
pkgver=1.1
pkgrel=8
pkgdesc='Cross platform text-to-speech.'
arch=('any')
url='https://pypi.org/project/pyttsx/#files'
license=('BSD')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('3a01de993ebdf2fd2b3f79a8ee8852aa6bda54041041784802cf06d3939c0e63a4b161a0969a94e659e67ec65c82d36fa8977ef1b6dd55c31c6d853f06a4b504')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

