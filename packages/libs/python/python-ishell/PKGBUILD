# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-ishell
_pkgname=${pkgname#python-}
pkgver=0.1.8
pkgrel=4
pkgdesc='Build Interactive Shells with Python.'
arch=('any')
url='https://pypi.python.org/pypi/ishell/'
license=('MIT')
depends=('python' 'python-future')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('9be7064ff46525daa70dc46d26f600451f4ae1d9fba083954a9620b3df93790d55f5f8d4345699df544914dbc677a7a89d897dac281a0bae9c40f0c382c26426')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

