# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pymiproxy
_pkgname=${pkgname#python-}
pkgver=1.0
pkgrel=5
pkgdesc='Micro Interceptor Proxy - a simple MITM HTTP/S proxy.'
arch=('any')
url='https://pypi.org/project/pymiproxy/#files'
license=('GPL-1.0-or-later')
depends=('python' 'python-pyopenssl')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('0218697cb00908f928079890ca93a5b5e31fd7d90194c52501bbb28260d80863daac66fd234f6f930d484f3a96b924975c6fed8b43f9987d66fe2e23f53ade4c')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

