# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-delegator
_pkgname=${pkgname#python-}
pkgver=0.0.3
pkgrel=8
pkgdesc="Python implementation of Rubyâ€™s delegate.rb."
arch=('any')
url='https://pypi.org/project/delegator/#files'
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('851a0097eeca08cd3c703e38caba8791c70c774fbccb7311f753c4c3c0c4aa953d67851057cf30d62ef68642df00234d78ec351af267c4eaccc93a2739fd76ad')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

