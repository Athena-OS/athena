# Credits BlackArch ( https://blackarch.org )
# See COPYING for license details.

pkgname=python-flask-restplus
_pkgname=${pkgname#python-}
pkgver=0.13.0
pkgrel=5
pkgdesc='Fully featured framework for fast, easy and documented API development with Flask.'
arch=('any')
url='https://pypi.org/project/Flask-restplus/#files'
license=('BSD')
depends=('python' 'python-flask' 'python-aniso8601' 'python-pytz'
         'python-six' 'python-jsonschema')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('d44d18af66b7999e3cc6197cc491fac36c4feda39de61155ea1f0bf438ba9a7a8bf89368ac9343313d9399e14876ebd7d169e7e333fb7f7749b9f5e1edb09151')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

