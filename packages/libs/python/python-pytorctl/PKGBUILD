# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-pytorctl
pkgname=('python2-pytorctl' 'python-pytorctl')
_pkgname=pytorctl
pkgver=c8fcb25
pkgrel=1
pkgdesc='A Python Tor controller with extensions to support path building and various constraints on node and path selection, as well as statistics gathering.'
arch=('any')
url='https://gitlab.torproject.org/tpo/network-health/pytorctl/'
license=('custom:unknown')
makedepends=('git' 'python2-setuptools' 'python-setuptools')
optdepends=('python2-elixir: SQL Support')
conflicts=('pytorctl')
provides=('pytorctl')
source=("git+https://gitlab.torproject.org/tpo/network-health/$_pkgname.git"
        'setup.py')
sha512sums=('SKIP'
            '78ba127cfd4f8964236acc41e48e97f1cf7d66c47300fee72e794a74686b66bbde0ce8f252710d4aa4f43a0519d0912e270839ef227179011d2b1abe01f58e4b')

pkgver() {
  cd $_pkgname

  git describe --always | sed 's|-|.|g'
}

package_python2-pytorctl() {
  depends=('python2' 'python2-geoip')

  install -dm 755 "$pkgdir/usr/share/"{licenses,doc}"/${pkgname[0]}"

  install -Dm 644 "$srcdir/$_pkgname/README" \
    "$pkgdir/usr/share/doc/${pkgname[0]}"

  install -Dm 644 "$srcdir/$_pkgname/LICENSE" \
    "$pkgdir/usr/share/licenses/${pkgname[0]}/LICENSE"


  python2 setup.py install --root="$pkgdir" --optimize=1
}

package_python-pytorctl() {
  depends=('python' 'python-geoip')

  install -dm 755 "$pkgdir/usr/share/"{licenses,doc}"/${pkgname[1]}"

  install -Dm 644 "$srcdir/$_pkgname/README" \
    "$pkgdir/usr/share/doc/${pkgname[1]}"

  install -Dm 644 "$srcdir/$_pkgname/LICENSE" \
    "$pkgdir/usr/share/licenses/${pkgname[1]}/LICENSE"

  python setup.py install --root="$pkgdir" --optimize=1
}

