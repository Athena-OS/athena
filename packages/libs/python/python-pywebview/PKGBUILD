# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pywebview
_pkgname=${pkgname#python-}
pkgver=5.1
pkgrel=1
pkgdesc='A cross-platform lightweight native wrapper around a web view component.'
arch=('any')
url='https://pypi.org/project/pywebview/#files'
license=('BSD')
depends=('python' 'python-pyqt5' 'python-pyqtwebengine' 'python-qtpy'
         'python-bottle' 'python-cryptography' 'python-typing_extensions')
makedepends=('python-build' 'python-pip')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('395243c34ce147970ac351bade647ad1c57d844fee390764097b7ec7f02562d298e9239eb76d0606394419e1e06d6c28d2cd58f1f10d46c5a4924759dac87aa0')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}
