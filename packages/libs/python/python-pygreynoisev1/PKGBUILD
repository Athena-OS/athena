# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pygreynoisev1
_pkgname=${pkgname#python-}
pkgver=0.1
pkgrel=5
pkgdesc='Python wrapper around the GreyNoise APO.'
arch=('any')
url='https://pypi.org/project/pygreynoisev1/#files'
license=('MIT')
depends=('python' 'python-requests')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('5ddcd409387669d6ce5bc2c5feb1540194d70905dee7b0289cfdfd19e8e6cb3dbd5fecd6ed0c195fc2421e01b749b2264b218607cd9ed20cc47952dd70eeeb9b')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

