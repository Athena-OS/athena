# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-secure
_pkgname=${pkgname#python-}
pkgver=1.0.1
pkgrel=1
pkgdesc='A lightweight package that adds optional security headers and cookie attributes for Python web frameworks.'
arch=('any')
url='https://pypi.org/project/secure/#files'
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('7a34c76b0a879e299ddec8c4fdbe7725a855812325f5fa3cc6f8fd88f8b8728656f5d9e6f49334a3a0b7897cdbb977376703129077ff0c044c4cd819ec835526')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

