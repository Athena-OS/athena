# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-verboselogs
_pkgname=${pkgname#python-}
pkgver=1.7
pkgrel=5
pkgdesc="Verbose logging level for Python's logging module."
arch=('any')
url='https://pypi.org/project/verboselogs/#files'
license=('Apache-2.0')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('13e96ac21e1cb5c68428b348bab63fc4d4ad34eaffe8e1554c05380f9115d2a3ccc6fa2e0a5ca8941e73bfb06537a3e46f29880e7ba3f47da0284279a9740211')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

