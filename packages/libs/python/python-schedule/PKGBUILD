# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-schedule
_pkgname=${pkgname#python-}
pkgver=1.2.2
pkgrel=1
pkgdesc='Job scheduling for humans.'
arch=('any')
url='https://pypi.org/project/schedule/#files'
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('e0720e84c6517821f074c0d37a3ec6bc923eb729e19223b7efdd8250dda4ed83347e3b0d5efd07bb67b1716bc3fd992547ca4b57b33aaaa6e70262f0ea8fcf97')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

