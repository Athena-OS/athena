# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-rdpy
_pkgname=${pkgname#python-}
pkgver=1.3.2
_pyver=3.13
pkgrel=17
epoch=1
pkgdesc='Remote Desktop Protocol in Twisted Python.'
arch=('any')
url='https://pypi.org/project/rdpy/#files'
license=('BSD')
depends=('python' 'python-pyopenssl' 'python-rsa' 'python-twisted'
         'python-pyasn1' 'python-service-identity' 'python-qt4reactor')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('66dcafc975da7ecf2359fe1c2297dd2a219f54d405f47a045067dc34f3e35b18198bf7f23474523875b9623e1b79600a371ca6fd254c5c6886118668e03edd2a')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  rm -f \
    "$pkgdir/usr/lib/python$_pyver/site-packages/rle.cpython-311-x86_64-linux-gnu.so"
}

