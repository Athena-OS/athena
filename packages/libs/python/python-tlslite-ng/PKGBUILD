# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-tlslite-ng
_pkgname=tlslite_ng
pkgver=0.8.1
pkgrel=1
epoch=1
pkgdesc='Pure python implementation of SSL and TLS.'
arch=('any')
url='https://pypi.org/project/tlslite-ng/#files'
license=('Apache-2.0')
depends=('python' 'python-ecdsa')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('253456b339a8e213769fcfc61afa11cb2ab2038fceed304800eaa11fb95459373a5dfec6eca208c0e6c32c32402691f7462d45a44a0482f61b74c7ca820ef3c5')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

