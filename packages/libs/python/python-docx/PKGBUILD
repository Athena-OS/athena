# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-docx
_pkgname=${pkgname#python-}
pkgver=0.2.4
pkgrel=9
pkgdesc='The docx module creates, reads and writes Microsoft Office Word 2007 docx files.'
arch=('any')
url='https://pypi.python.org/pypi/docx/'
license=('MIT')
depends=('python' 'python-lxml' 'python-pillow')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://pypi.python.org/packages/4a/8e/5a01644697b03016de339ef444cfff28367f92984dc74eddaab1ed60eada/$_pkgname-$pkgver.tar.gz")
sha512sums=('f8825462726121d52affe0aa8d026891179f5209835631181f810af142fd0a59fb7d3f98733a84c99da8def56a586f945a364fea7b198cc8ba20316715ff3525')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

