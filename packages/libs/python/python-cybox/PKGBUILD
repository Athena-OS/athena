# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-cybox
_pkgname=${pkgname#python-}
pkgver=2.1.0.21
pkgrel=3
epoch=1
pkgdesc='A Python library for parsing and generating CybOX 2.1.0 content.'
arch=('any')
url='https://github.com/CybOXProject/python-cybox'
license=('BSD')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('9850e0bf21314626301c862fd97f32745fa053a1f85fa7d0e3ec9c832714a5cd778a1727e2700c83f19350058695d2b865b4aca4fa1f48e3169524916f32684a')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

