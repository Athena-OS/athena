# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-colorizer
_pkgname=${pkgname#python-}
pkgver=0.1.7
pkgrel=9
pkgdesc="Console colorizer, which acts like grep but paint each match in it's own color."
arch=('any')
url='https://github.com/svetlyak40wt/colorizer/'
license=('BSD')
depends=('python' 'python-termcolor')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('fdbed0e5ce986e83197ceb7c09e2a9fd73b87e9e3de1f57b5595e7751dac62bd434380ae003d1f29faf5a3043a2f97a47a566d9ce88963d136c7986e288a7220')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

