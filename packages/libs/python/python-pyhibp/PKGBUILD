# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyhibp
_pkgname=${pkgname#python-}
pkgver=4.2.0
pkgrel=3
pkgdesc="An interface to Troy Hunt's 'Have I Been Pwned' public API."
arch=('any')
url='https://pypi.org/project/pyHIBP/#files'
license=('AGPL-3.0-or-later')
depends=('python-requests' 'python-six')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('407ed0f260c03f28415d2da531be0105ef95025e734bafc2d07fdad7f09138f4bb84acef83cdc463e28c4cb601e3a8aa64d0d09b9c14033d69154c3c587334df')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

