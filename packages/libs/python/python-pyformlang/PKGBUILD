# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyformlang
_pkgname=${pkgname#python-}
pkgver=323.9d576c8
pkgrel=1
pkgdesc='Python library to manipulate formal grammar.'
arch=('any')
url='https://github.com/Aunsiels/pyformlang'
license=('MIT')
depends=('python' 'python-networkx' 'python-numpy' 'python-pydot')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/Aunsiels/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $_pkgname

  python setup.py build
}

package() {
  cd $_pkgname
    
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

