# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pysmb
_pkgname=${pkgname#python-}
pkgver=1.2.10
pkgrel=1
pkgdesc='An experimental SMB/CIFS library written in Python to support file sharing between Windows and Linux machines.'
arch=('any')
url='https://pypi.org/project/pysmb/#files'
license=('GPL-1.0-or-later')
depends=('python' 'python-pyasn1')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.zip")
sha512sums=('5b7ef37ef491df3e4ab1e255cfd6f2a5d54e67332328d3c46fe131bf1920557dbfa402d54465a580b8b41f349b4abfd4026ec9cbdea9b3ab82c1247aa9d847d7')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

