# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-nfqueue
pkgver=v0.6.r1.g9bf0c34
pkgrel=1
pkgdesc="Python bindings for libnetfilter_queue by Pierre Chifflier"
url="https://github.com/chifflier/nfqueue-bindings"
arch=('x86_64' 'aarch64')
license=('GPL-1.0-or-later')
depends=('python2' 'libnetfilter_queue')
makedepends=('python2-setuptools' 'cmake' 'git' 'swig')
source=("git+https://github.com/chifflier/nfqueue-bindings.git"
        python-support.patch)
sha512sums=('SKIP'
            '2504555b46fd1b9ac72091875f98add48f3218986e9683655550dd42d3d434dc42908235952ea686aeffa6fad377a3a694e527af3b31171549f015700fafcd60')

pkgver() {
  cd "nfqueue-bindings"

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "nfqueue-bindings"

  patch -p1 -i ../python-support.patch

  # I don't feel like editing the patch.
  sed -i 'i\#include <stdint.h>' nfq.h
  sed -i 's/dist-packages/site-packages/' python/CMakeLists.txt
}

package() {
  cd "nfqueue-bindings"

  # Examples.
  install -dm 755 -p "$pkgdir/usr/share/doc/nfqueue-bindings/examples"
  install -m644 examples/*.py "$pkgdir/usr/share/doc/nfqueue-bindings/examples"

  # Library.
  make PREFIX="$pkgdir/usr" install
}

