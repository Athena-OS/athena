# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-sha3
_pkgname=${pkgname#python-}
pkgver=0.2.1
pkgrel=7
pkgdesc='SHA-3 implementation for Python.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/sha3/#files'
license=('custom:unknown')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('13a0e52fb14c1a640d736781084963d0b26bab3d20864911e6446c0d87b422f1b943b224c35cb59410ca322da6c2e167a6638ff339b57c178c2f01f28f00d306')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

