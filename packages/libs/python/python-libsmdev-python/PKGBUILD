# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-libsmdev-python
_pkgname=${pkgname#python-}
pkgver=20240505
pkgrel=1
pkgdesc='Python bindings module for libsmdev.'
arch=('x86_64' 'aarch64')
url='https://pypi.python.org/pypi/libsmdev-python/'
license=('LGPL-3.0-or-later')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('605484fd368715e3c5c5e2b3569ce616cc3157b47bacc81a81285bbd29bc05276ada580fb2848142be7535968b2180d05e0a0ffaa11d062b4e97a22c61972541')

build() {
  cd "libsmdev-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "libsmdev-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

