# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-libsmdev-python
_pkgbase=${pkgbase#python-}
pkgname=('python2-libsmdev-python' 'python-libsmdev-python')
pkgver=20221028
pkgrel=2
pkgdesc='Python bindings module for libsmdev.'
arch=('x86_64' 'aarch64')
url='https://pypi.python.org/pypi/libsmdev-python/'
license=('LGPL-3.0-or-later')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgbase::1}/$_pkgbase/$_pkgbase-$pkgver.tar.gz")
sha512sums=('5b67b44a8a763fd592ac0ea1fdf5a76ad0f4ea0366588d39397c4446f1015a64049a45ed1afb7ae7202d64b7146c4a7f625a66154cc35712e52feb3443450b10')

prepare() {
  cp -a "libsmdev-$pkgver"{,-2}
}

build() {
  cd "libsmdev-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/libsmdev-$pkgver"

  python setup.py build
}

package_python2-libsmdev-python() {
  depends=('python2')

  cd "libsmdev-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-libsmdev-python() {
  depends=('python')

  cd "libsmdev-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

