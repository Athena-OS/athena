# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-symautomata
_pkgname=${pkgname#python-}
pkgver=0.0.12
pkgrel=7
pkgdesc='A python framework for working with Automata.'
arch=('any')
url='https://pypi.python.org/pypi/symautomata/'
license=('custom:unknown')
depends=('python' 'python-dateutil' 'python-six')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('c2b933f7861eec5be24fd82740206e62f37b5e016a7b9214b618fd5a0be2d0e169547e78e63cbd4c2623b43d74aeaac200d95c49baff9e24bc99b52577f1ae1f')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

