# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-py3270
pkgname=('python2-py3270' 'python-py3270')
_pkgname=py3270
pkgver=0.3.5
pkgrel=1
pkgdesc='A Python interface to x3270, an IBM 3270 terminal emulator.'
arch=('any')
url='https://pypi.org/project/py3270/#files'
license=('BSD')
depends=('python' 'python-six')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('5a278de78ba12c9feb5627b6a4b71a297643c330145a9d61dc74d3c9b6218ca53d4b2db2adc6e5487c8dc9066238b466c430e018fa04e758a3061ac819dfb450')

prepare() {
  cp -a "$_pkgname-$pkgver"{,-2}
}

build() {
  cd "$_pkgname-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

package_python2-py3270() {
  depends=('python2' 'python2-six')

  cd "$_pkgname-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-py3270() {
  depends=('python' 'python-six')

  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}
