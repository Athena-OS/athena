# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyvex
_pkgname=${pkgname#python-}
pkgver=1145.ea47010
pkgrel=1
pkgdesc='A Python interface to libVEX and VEX IR.'
arch=('x86_64' 'aarch64')
url='https://github.com/angr/pyvex'
license=('BSD-2-Clause')
depends=('python' 'python-pycparser' 'python-cffi' 'python-archinfo'
         'python-bitstring' 'python-future' 'python-requests')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/angr/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  cd $_pkgname

  git submodule update --init
}

build() {
  cd $_pkgname

  python setup.py build
}

package() {

  cd $_pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

