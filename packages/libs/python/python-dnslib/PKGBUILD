# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-dnslib
_pkgname=${pkgname#python-}
pkgver=0.9.25
pkgrel=1
pkgdesc='Simple library to encode/decode DNS wire-format packets.'
arch=('any')
url='https://pypi.org/project/dnslib/#files'
license=('BSD')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('d3bc1b4af9a6a64521fc018bb48fa4f9fdcd694bbb95a42365266a655029f225850de2f6a5227cc29163edbae842328aebfb55730925f884ddd39aedc029e8db')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

