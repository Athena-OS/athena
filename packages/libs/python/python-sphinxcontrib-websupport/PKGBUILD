# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-sphinxcontrib-websupport
_pkgbase=${pkgbase#python-}
pkgname=('python2-sphinxcontrib-websupport' 'python-sphinxcontrib-websupport')
pkgver=1.2.4
pkgrel=1
pkgdesc='Sphinx API for Web Apps'
url='https://pypi.org/project/sphinxcontrib-websupport/#files'
arch=('any')
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgbase::1}/$_pkgbase/$_pkgbase-$pkgver.tar.gz")
sha512sums=('d1ccfb6a9ecfa6adbdf2298f4ded345cacefbc5a450b8dafc6faadbcd0dd79a8dbefcab4d1cedf34c65fc271aab754392bea8ab8ae82852323784a9e1717914c')

prepare() {
  cp -a "$_pkgbase-$pkgver"{,-2}
}

build() {
  cd "$_pkgbase-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/$_pkgbase-$pkgver"

  python setup.py build
}

package_python2-sphinxcontrib-websupport() {
  depends=('python2')

  cd "$_pkgbase-$pkgver-2"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

package_python-sphinxcontrib-websupport() {
  depends=('python')

  cd "$_pkgbase-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

