# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-duckpy
_pkgname=${pkgname#python-}
pkgver=3.2.0
pkgrel=1
pkgdesc='A simple Python module for searching on DuckDuckGo.'
arch=('x86_64' 'aarch64')
url='https://github.com/AmanoTeam/duckpy'
license=('MIT')
depends=('python' 'python-beautifulsoup4' 'python-httpx')
makedepends=('python-build' 'python-pip')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('e14d6cf891e500122018c39528a0dde5763b9183802d27106aeed79740c06ba41c0def373bea854f1d4355a85480e73f9289363f3337a8e67600038caab8aab1')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}
