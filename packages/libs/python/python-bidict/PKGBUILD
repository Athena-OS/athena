# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-bidict
_pkgname=${pkgname#python-}
pkgver=0.23.1
pkgrel=1
pkgdesc='The bidirectional mapping library for Python.'
arch=('any')
url='https://pypi.org/project/bidict/#files'
license=('MPL2')
depends=('python')
makedepends=('python-build' 'python-pip')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('fd0d1b72a19cfa7d3705c550005a346b14ba0c2196ef8f3e72377ff130e5d234009823d24d2613adf0cf75b5482810e6aa7a3b3d22021024ad274663a67d43ac')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}
