# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-proxy-db
_pkgname=${pkgname#python-}
_pyver=3.13
pkgver=0.3.1
pkgrel=9
pkgdesc='Manage free and private proxies on local db for Python Projects.'
arch=('any')
url='https://pypi.org/project/proxy-db/#files'
license=('GPL-3.0-or-later')
depends=('python' 'python-requests' 'python-click' 'python-sqlalchemy'
         'python-beautifulsoup4' 'python-geoip2')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('d39d3fcf810330413c8742169fe6f355fe1828afea7b530641ca198b1284b36894a90b934c39ab86535b7b05b8c5d078d1249f9e33bf623f238daecd95175c56')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"
}

