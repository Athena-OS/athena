# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-tracer
_pkgname=${pkgname#python-}
pkgver=2.0.0
pkgrel=1
pkgdesc='Centralized sys.settrace management.'
arch=('any')
url='https://pypi.python.org/pypi/tracer/'
license=('GPL-3.0-or-later')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('e25856063df777edce8b0bd49a273a7d77fab60d1114b70a22274e8fad9de5367d2c30d20eabdd48b3a66887c2542967ee6d9219c2d1d4fcbd2898294a6ed665')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

