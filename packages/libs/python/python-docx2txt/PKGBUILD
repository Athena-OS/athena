# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-docx2txt
_pkgname=${pkgname#python-}
pkgver=0.8
pkgrel=5
pkgdesc='A pure python-based utility to extract text from docx files.'
arch=('any')
url='https://pypi.org/project/docx2txt/'
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/docx2txt-0.8.tar.gz")
sha512sums=('9e2c67290a345bac9090898a778d8ee254e38cebf6edbcfc8e1232b0d4f2769d7e202a9504f80da200955d35e448c8b0fd111438a992bf880bff395f50b0b51a')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

