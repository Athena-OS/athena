# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-archinfo
_pkgname=${pkgname#python-}
pkgver=9.2.102
pkgrel=1
pkgdesc='Classes with architecture-specific information useful to other projects.'
arch=('any')
url='https://pypi.org/project/archinfo/#files'
license=('BSD')
depends=('python' 'capstone' 'python-pyelftools')
makedepends=('python-build' 'python-pip')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('9dadc6f6e69484d8a5df031e059c03738c86508cc1136c6f2eae1d7d8695b8d72b188801bbef2916bb59342e7ee59ad68b378db2cf14b91163330d79b0c9494a')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}
