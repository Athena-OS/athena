# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-exscript
_pkgname=${pkgname#python-}
pkgver=v2.6.r30.g9d5b035
pkgrel=1
pkgdesc='A Python module making Telnet and SSH easy.'
arch=('any')
url='https://github.com/knipknap/exscript'
license=('GPL-2.0-or-later')
depends=('python' 'python-pycryptodome' 'python-paramiko' 'python-future')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("git+https://github.com/knipknap/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $_pkgname

  python -m build --wheel --no-isolation
}

package() {
  cd $_pkgname

  python -m installer --destdir="$pkgdir" dist/*.whl
}
