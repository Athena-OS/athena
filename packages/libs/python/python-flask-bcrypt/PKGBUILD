# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-flask-bcrypt
_pkgname='Flask-Bcrypt'
pkgver=1.0.1
pkgrel=2
pkgdesc='Bcrypt hashing for Flask.'
arch=('any')
url='https://pypi.org/project/Flask-Bcrypt/'
license=('BSD')
depends=('python' 'python-flask' 'python-bcrypt')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/Flask-Bcrypt-$pkgver.tar.gz")
sha512sums=('e7df01d5e2e193e80e12c8cf07fe01a487ab1b3a2f9175533e653636c2a4aa7a4175cbfc456b3251b7c536ab72f9b88c6d407163741807c9965c2b6d842c8203')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

