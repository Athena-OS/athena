# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-wakeonlan
_pkgname=${pkgname#python-}
pkgver=3.1.0
pkgrel=1
pkgdesc='A small python module for wake on lan.'
arch=('any')
url='https://pypi.org/project/wakeonlan/#files'
license=('WTFPL')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('92d1e5e5b21a6df10ae19436610fd493fd850b55d6dd03e8b93c6a0be493ecc4d5b523ca3668437dcabb7f8883fa1ab2f0f30664c48c3f2da2f4cd8bb1816246')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

