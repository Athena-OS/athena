# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-libregf-python
_pkgname=${pkgname#python-}
pkgver=20240421
pkgrel=1
pkgdesc='Python bindings module for libregf.'
arch=('x86_64' 'aarch64')
url='https://pypi.python.org/pypi/libregf-python/'
license=('LGPL-3.0-or-later')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('2b126b530cd4735923a837d57f61f93132281fcbc7db1fd44f9b168a4f4863f8b4f9ca90d445515cab60815d4b94a92676b4dbd83b0b57223c502f9f411ae635')

build() {
  cd "libregf-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "libregf-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

