# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pygoogle
_pkgname=${pkgname#python-}
pkgver=0.6
pkgrel=8
pkgdesc='This module lets you search the Google search engine programmatically.'
arch=('any')
url='https://pypi.python.org/pypi/pygoogle/'
license=('BSD')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('ed3246da24b16942ff13ab98f03082bcf545b36ca6c30b9d473d6a225b6f6b859187052388f5cf89e9e272a0101d6c4efda35365d19611a079a8ab94b0b3b2b3')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

