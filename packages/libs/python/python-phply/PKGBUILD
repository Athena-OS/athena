# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-phply
_pkgname=${pkgname#python-}
pkgver=1.2.6
_pyver=3.13
pkgrel=5
pkgdesc='PHP parser written in Python using PLY.'
arch=('any')
url='https://pypi.org/project/phply/#files'
license=('BSD')
depends=('python' 'python-ply')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('658e48d67b634185bb20637ed92b0e0ce83adaf62a584bcd9caae377bcdd904193398ca7b3947cbfd8998ae10b53ffc0dcf60401a558ab12c6c1b2024c50f284')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  rm -rf "$pkgdir/usr/lib/python$_pyver/site-packages/tests"
}

