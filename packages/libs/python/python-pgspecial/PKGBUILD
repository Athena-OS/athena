# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pgspecial
_pkgname=${pkgname#python-}
pkgver=2.1.3
pkgrel=1
pkgdesc='Meta-commands handler for Postgres Database.'
arch=('any')
url='https://pypi.org/project/pgspecial/#files'
license=('BSD')
depends=('python' 'python-click' 'python-sqlparse')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('67f7376cbad896a29e9ae76fd1f78b1e43069dc6a648eb0e68048f22420c606da437ef4b9f1b149a748161e12f061ccff2fd0682e7f2b9d2ac41003bc062b768')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

