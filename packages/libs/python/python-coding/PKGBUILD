# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-coding
_pkgname=${pkgname#python-}
pkgver=0.3
pkgrel=8
pkgdesc='An answer to the question of python enums.'
arch=('any')
url='https://pypi.python.org/pypi/coding/'
license=('MIT')
depends=('python' 'python-pylint' 'python-nose')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.bz2")
sha512sums=('2cb37d5797f792a10469074bd2cbaafb411fcfbc9d0c574ac0bcac0c8705324df24c256ab2cdca699967267aca0894b21d274bae335d49840e049ef6aab8db22')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

