# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgbase=python-cjkwrap
_pkgbase=${pkgbase#python-}
pkgname=('python2-cjkwrap' 'python-cjkwrap')
pkgver=2.2
pkgrel=1
pkgdesc='Generate a formatted text table using ASCII characters.'
arch=('any')
url="https://pypi.python.org/pypi/CJKwrap/"
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgbase::1}/$_pkgbase/CJKwrap-$pkgver.tar.gz")
sha512sums=('59e0139daadad4d0397f0684a0c1abd5803d133e90ff7ced0adb703be02b20e430bb96a6777227640e55aca6d1aaea796bfc79b5d5b3379953e38323003cbde0')

prepare() {
  cp -a "CJKwrap-$pkgver"{,-2}
}

build() {
  cd "CJKwrap-$pkgver-2"

  python2 setup.py build

  cd "$srcdir/CJKwrap-$pkgver"

  python setup.py build
}

package_python2-cjkwrap() {
  depends=('python2')

  cd "CJKwrap-$pkgver-2"

  python2 setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}

package_python-cjkwrap() {
  depends=('python')

  cd "CJKwrap-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" -O1 --skip-build
}
