# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-weakrefmethod
_pkgname=${pkgname#python-}
pkgver=1.0.3
pkgrel=5
pkgdesc='A WeakMethod class for storing bound methods using weak references.'
url='https://pypi.org/project/weakrefmethod/#files'
arch=('any')
license=('Python')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('c68dc806904eb954319984003506eb9b552005ce04722c6a3eb123ecde8f4413f9f9d114f0923c6f6ae2e25b4dbaf8c225c070a1720a6563cf1517f2411a60d5')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

