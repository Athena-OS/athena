# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pyip
_pkgname=${pkgname#python-}
pkgver=0.7
pkgrel=4
pkgdesc='Assemble/disassemble raw ip packet.'
arch=('any')
url='https://pypi.org/project/pyip/#files'
license=('PSF')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('190319a508af8069194cca7d954a87cd4777f2d4cabf199a0c52abe7a54dea65f541b24f42d10d52b325d40b0ad29f903ff5b28118a05a5e57c4f16d78a48f01')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

