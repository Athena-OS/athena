# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-bitstring
_pkgname=${pkgname#python-}
pkgver=4.2.2
pkgrel=1
pkgdesc='Simple construction, analysis and modification of binary data.'
arch=('any')
url='https://pypi.org/project/bitstring/#files'
license=('MIT')
depends=('python' 'python-bitarray')
makedepends=('python-build' 'python-pip' 'unzip')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('4700b45c9a56c21a09d61c464a4b2de899bb1cbdb1c5057e5c0f7c388adbda8feb260bdd56f073e53e256c39825cf7cd4b105bf9f57dd6d9d4916717941ff4bf')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --outdir="$startdir/dist"
}

package() {
  cd "$_pkgname-$pkgver"

  pip install \
    --verbose \
    --disable-pip-version-check \
    --no-warn-script-location \
    --ignore-installed \
    --no-compile \
    --no-deps \
    --root="$pkgdir" \
    --prefix=/usr \
    --no-index \
    --find-links="file://$startdir/dist" \
    $_pkgname
}
