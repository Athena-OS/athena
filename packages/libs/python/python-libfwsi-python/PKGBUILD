# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-libfwsi-python
_pkgname=${pkgname#python-}
pkgver=20240423
pkgrel=1
pkgdesc='Python bindings module for libfwsi.'
url='https://pypi.org/project/libfwsi-python/#files'
arch=('x86_64' 'aarch64')
license=('LGPL-3.0-or-later')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('7125767419fae3638726814574bbc4232f28b2f9834fa77128b884f0d0c77fa694c1420428238b09b4381627fad802474802ce773d31b6db29577b0b3353e251')

build() {
  cd "libfwsi-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "libfwsi-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

