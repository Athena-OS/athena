# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-maclookup
_pkgname=${pkgname#python-}
pkgver=1.0.3
pkgrel=5
pkgdesc='Python client library for macaddress.io API.'
arch=('any')
url='https://pypi.org/project/maclookup/#files'
license=('MIT')
depends=('python' 'python-future' 'python-dateutil')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('20dfc1f9ac926f0cc50a8abb49fa5a2665e7deb20109ff082be761a55390e72cc3a2281549900a1641ed2986bed27e5d5957e6d693d701dc969ddd8a04cc80e3')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

