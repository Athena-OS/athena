# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-libvhdi-python
_pkgname=${pkgname#python-}
pkgver=20240509
pkgrel=1
pkgdesc='Library and tools to access the Virtual Hard Disk (VHD) image format.'
url='https://pypi.org/project/libvhdi-python/'
arch=('x86_64' 'aarch64')
license=('LGPL-3.0-or-later')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('377d9e05abad08b1c312d67eaa7ee990cec67ed7a5d5ff532d82f5b37b5968f89cd6a5319a2f0baf0fd64117d669bd2457a244e8e6db1c9befea59264d9dedc6')

build() {
  cd "libvhdi-$pkgver"

  python setup.py build
}

package() {
  cd "libvhdi-$pkgver"

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}
