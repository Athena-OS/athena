# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-python-evt
_pkgname=${pkgname#python-}
pkgver=0.1
pkgrel=9
pkgdesc='Pure Python parser for classic Windows event log files (.evt).'
arch=('any')
url='https://pypi.org/project/python-evt/#files'
license=('BSD')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('92adbf58c07c7c83b9850013955654c998b0633cc5d565cf7e55d65803dcf335066f8e017a1d77eb6c47798d6faea83e08f1deefc72cefd2846aa2a1dc57eadd')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

