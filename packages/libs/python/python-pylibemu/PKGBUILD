# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pylibemu
_pkgname=${pkgname#python-}
pkgver=1.0
pkgrel=1
pkgdesc='A Libemu Cython wrapper.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pylibemu/#files'
license=('GPL-2.0-or-later')
depends=('python' 'libemu')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('3425d3c3a57cb5a970cdc80a1479af9a192e179544ec403c4417ffc6ca246ff0d5cc1ed752401b9796dfca63ea3ac6d015e09d87247c6f78d0fd7100b040849b')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

