# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pybinaryedge
_pkgname=${pkgname#python-}
pkgver=0.5
pkgrel=5
pkgdesc='Python 3 Wrapper for the binary edge API https://www.binaryedge.io/.'
arch=('any')
url='https://pypi.org/project/pybinaryedge/#files'
license=('MIT')
depends=('python' 'python-requests')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('3d8029effa98089a356d53dac9eeefadc888303b89ff0cb62cbbab3898ca3a87194393afe3ea4f6fcf7eb8aa26a97807292b2ebe6283ba8c3dbb2e07d8be2f3f')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

