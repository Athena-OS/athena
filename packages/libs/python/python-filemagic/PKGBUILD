# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-filemagic
_pkgname=${pkgname#python-}
pkgver=1.6
_pyver=3.13
pkgrel=12
pkgdesc='A Python API for libmagic, the library behind the Unix file command.'
url='https://pypi.org/project/filemagic/#files'
arch=('any')
license=('Apache-2.0')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('2b33b55467d14697ddcee2b1cb69eb41dd51474d2d8d095873f6f6609d77efa295a91bc9b976c9af22983a7cc801e1ea1e55163c18c5b6a41c4e1ff9cf73bb47')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

