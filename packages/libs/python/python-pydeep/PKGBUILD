# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pydeep
_pkgname=${pkgname#python-}
pkgver=0.4
pkgrel=6
pkgdesc='This is the Python interface to ssdeep.'
arch=('x86_64' 'aarch64')
url='https://pypi.org/project/pydeep/#files'
license=('custom:PSF')
depends=('python' 'ssdeep')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('f924eb61dfba173704b7d8345bf14a8a01a78229b2733309861869f69cd1807325512ada440c7d7ff247fe2ceeca3ac3df2203cb55a5671b658734cb550146f1')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

