# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-click-shell
_pkgname=${pkgname#python-}
pkgver=2.1
pkgrel=3
pkgdesc='An extension to click that easily turns your click app into a shell utility.'
arch=('any')
url='https://pypi.org/project/click-shell/#files'
license=('BSD')
depends=('python' 'python-click')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('dbe435d6a3b51e587fe9890f5edd56e53d0133a4e7b33e634172cad049ed10ee68f29d0d22e2b48f8563d8874ed1c62b1f9377146369b51fb7062ba7652cd11c')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

