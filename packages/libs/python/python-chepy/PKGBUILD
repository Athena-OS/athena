# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-chepy
_pkgname=${pkgname#python-}
pkgver=7.0.0
pkgrel=1
pkgdesc='Chepy is python lib/cli equivalent of the awesome CyberChef tool.'
arch=('any')
url='https://pypi.org/project/chepy/#files'
license=('GPL-3.0-or-later')
depends=('python' 'python-colorama' 'python-crccheck' 'python-decorator'
         'python-emoji' 'exrex' 'python-fire' 'python-hashid' 'python-hexdump'
         'python-jsonpath-rw' 'python-jsonpickle' 'python-lz4' 'python-msgpack'
         'python-parsel' 'python-passlib' 'python-phpserialize' 'python-pillow' 
         'python-prompt_toolkit' 'python-pycipher' 'python-pycryptodome' 'python-pydash' 
         'python-pyjwt' 'python-pyopenssl' 'python-pyperclip' 'python-editor'
         'python-pyaml' 'python-regex' 'python-typing_extensions' 'python-ujson'
         'python-base58' 'python-docstring-parser' 'python-termcolor'
         'python-lazy-import')
makedepends=('python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('fcb1681a0021cd633f54b754c46d628a47d2d9344bc1d897e1e269d25cfa6672480f1386c9e09872472264c9257e77dae86b7454199c1e23d2ce1e3adf38897c')

prepare() {
  cd $_pkgname-$pkgver

  sed -i '8s/.*/fire/' requirements.txt
  sed -i '12s/.*/prompt_toolkit/' requirements.txt
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

