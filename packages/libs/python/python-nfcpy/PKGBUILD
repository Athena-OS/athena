# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-nfcpy
_pkgname=${pkgname#python-}
pkgver=1.0.4
pkgrel=1
pkgdesc='A Python module to read/write NFC tags or communicate with another NFC device.'
arch=('any')
url='https://github.com/nfcpy/nfcpy/'
license=('EUPL')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('486b271fc64e592c3ff724162df9f21a595dd9d4d55a51ba771b85378c9220166acf5c267ff87f3ceffe7a0a35c28e788e04a46df810da0cb53cef8484f1957e')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
