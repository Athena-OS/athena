# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-macholib
_pkgname=${pkgname#python-}
pkgver=1.16.3
pkgrel=1
pkgdesc='Mach-O header analysis and editing.'
arch=('any')
url='https://pypi.org/project/macholib/#files'
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('269f8aefe2686cdd22486f6c9d20a2c7034c38984886a1e235ab2285d13e7087286b8ea094704a31105c4a48b9b375a42cb2c65914c95e6a138b203310d17832')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

