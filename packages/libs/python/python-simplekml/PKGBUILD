# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-simplekml
_pkgname=${pkgname#python-}
pkgver=1.3.6
pkgrel=3
epoch=1
pkgdesc='KML generation with as little effort as possible.'
arch=('any')
url='https://pypi.org/project/simplekml/#files'
license=('GPL-1.0-or-later')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('0b589559141571f62936ac981c3970e64046169bd5444e1409594dcaf3245c674dfcaa35e607e9870b63b3eb806e5c7cd7a4600924dbb0680a03ff458e58bdef')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

