# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-unique-log-filter
_pkgname=unique_log_filter
pkgver=v0.1.0.r0.g648b3a5
pkgrel=1
pkgdesc='Simple tool to filter out duplicate lines from a log file.'
arch=('any')
url='https://github.com/twizmwazin/unique_log_filter'
license=('BSD-2-Clause')
depends=('python')
makedepends=('git' 'python-build' 'python-flit' 'python-installer' 'python-wheel')
source=("git+https://github.com/twizmwazin/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $_pkgname

  python -m build --wheel --no-isolation
}

package() {
  cd $_pkgname

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

