# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-mss
_pkgname=${pkgname#python-}
pkgver=10.0.0
pkgrel=1
pkgdesc='An ultra fast cross-platform multiple screenshots module in pure python using ctypes.'
url='https://pypi.org/project/mss/#files'
arch=('any')
license=('MIT')
depends=('python' 'python-argparse')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('f2ed2fcd927e51a90facd3f7eefa56e745ed5ccb04fcf46f66f3ad969dc2d5f28d87887950cdd1f552e36250376e4cd77d6c65607848f7768e952319c6197bdc')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

