# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-get-mac
_pkgname=${pkgname#python-}
pkgver=0.9.2
pkgrel=2
pkgdesc='Get MAC addresses of remote hosts and local interfaces.'
url='https://pypi.org/project/get-mac/#files'
arch=('any')
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('4306d47d521c9f1ae50221789e6ff6c932357abdc17633b403a0709d91e7714a74bdbba231c1ef20fdf2dc35c6ff4cddcc6f6d3203e2205bf48d439c995649f2')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

