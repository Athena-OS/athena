# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-py2neo
_pkgname=${pkgname#python-}
pkgver=2021.2.4
epoch=1
pkgrel=1
pkgdesc='Python client library and toolkit for Neo4j.'
arch=('x86_64' 'aarch64')
url='https://pypi.python.org/pypi/py2neo/'
license=('Apache-2.0')
depends=('python' 'python-certifi' 'python-click' 'python-colorama'
         'python-pygments' 'python-pytz' 'python-urllib3' 'python-neobolt'
         'python-neotime' 'python-prompt_toolkit')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('191a235c36200411c3b0c9ca2cdb740ca5c19c34a3b9da48ddc4f05debb4e46951988b993990258db85b1751eb896a4e484f2305eadfed67978446c95c3ff443')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

