# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-xdot
_pkgname=${pkgname#python-}
pkgver=1.4
pkgrel=1
pkgdesc='Interactive viewer for Graphviz dot files.'
arch=('any')
url='https://pypi.org/project/xdot/#files'
license=('LGPL-3.0-or-later')
depends=('python' 'python-gobject' 'python-numpy' 'python-packaging')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
conflicts=('xdot')
provides=('xdot')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('2aeec1ddc72f478524f1c19efe05a31f5aa112d1b814c7106c8b95e4cdbd06c29bc197fe6220696b522ed8b486f29b728584e4e977ae1ec0d92672675444c73a')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

