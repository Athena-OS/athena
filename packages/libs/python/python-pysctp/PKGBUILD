# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-pysctp
_pkgname=${pkgname#python-}
pkgver=0.7.2
pkgrel=4
pkgdesc='A python module for the SCTP protocol stack and library.'
arch=('any')
url='https://pypi.org/project/pysctp/#files'
license=('LGPL')
depends=('python' 'lksctp-tools')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
optdepends=('linux-headers' 'linux-lts-headers' 'linux-hardened-headers'
            'linux-rt-headers' 'linux-rt-lts-headers' 'linux-zen-headers')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('854906d309da7c8465c06e99ba8b812991d8bd65544623f9d9ce85bd512851dcaca676279e446db739d4eb7d4de95caed5eb06b21407256eea26e1804a784411')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  rm -rf "$pkgdir/usr/_sctp.h"
}

