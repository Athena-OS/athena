# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-google
_pkgname=${pkgname#python-}
pkgver=3.0.0
pkgrel=5
pkgdesc='Python bindings to the Google search engine.'
arch=('any')
url='https://pypi.org/project/google/#files'
license=('BSD')
depends=('python' 'python-beautifulsoup4')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('6d2722fd3c40d27ce67ac40864e1a8bd2cdb4dcc93b85bee295df9d858ad509abc9c191642963a00c8594a90cd0caacd4dd904c1d5770703d7605e4fd5384c9f')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

