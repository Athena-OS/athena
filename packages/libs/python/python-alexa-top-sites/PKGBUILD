# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python-alexa-top-sites
pkgver=5.98d6109
pkgrel=7
pkgdesc='Python library to fetch the top 1..N (where N < 1 million) sites from Alexa.'
arch=('any')
url='https://github.com/philarkwright/Alexa-Top-Sites'
license=('BSD')
depends=('python')
makedepends=('git' 'python-setuptools')
source=("$pkgname::git+https://github.com/philarkwright/Alexa-Top-Sites.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 \
    --skip-build
}

