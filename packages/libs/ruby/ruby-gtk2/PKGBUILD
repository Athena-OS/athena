# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.
#
# Old Generated by gem2arch (https://github.com/anatol/gem2arch)
# Old Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname="ruby-gtk2"
_pkgname=${pkgname#ruby-}
pkgver=3.4.3
pkgrel=4
pkgdesc='Ruby/GTK2 is a Ruby binding of GTK+-2.x.'
arch=('x86_64' 'aarch64')
url='https://rubygems.org/gems/gtk2'
license=('LGPL')
depends=('ruby' 'gtk2' 'ruby-atk' 'ruby-pango' 'ruby-gdk_pixbuf2')
makedepends=('ruby-native-package-installer')
options=(!emptydirs)
source=("https://rubygems.org/downloads/$_pkgname-$pkgver.gem")
noextract=("$_pkgname-$pkgver.gem")
sha512sums=('a995287fa25c5e5d71ff5856415c165b7e7baf01dfd4046892013bd597b6c7f0cd436f5db523810c3f50bcd20f6ed13bc0bb9c91140914d2a893a2d664a4260e')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  if [[ $CARCH == arm* ]] ; then
    gem install --no-user-install --ignore-dependencies --no-rdoc --no-ri \
      -n "$pkgdir/usr/bin" -i "$pkgdir$_gemdir" "$_pkgname-$pkgver.gem"
  else
    gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
      -n "$pkgdir/usr/bin" "$_pkgname-$pkgver.gem"
  fi

  rm "$pkgdir/$_gemdir/cache/$_pkgname-$pkgver.gem"
}
