# See COPYING for license details.

pkgname=python2-pylint
_pkgname=${pkgname#python2-}
pkgver=1.9.5
pkgrel=1
pkgdesc='Analyzes Python code looking for bugs and signs of poor quality.'
arch=('any')
url='https://pypi.org/project/pylint/#files'
license=('GPL-2.0-or-later')
depends=('python2' 'python2-astroid' 'python2-logilab-common' 'python2-tk')
makedepends=('python2-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('3f645974276c8776225e953be7cbfee85bc5f2496d962a69fe20304a38daa0dba2582ad5c3400c5ef18eb30c14116d198092fea6494d262a3c048d044a121f56')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}
