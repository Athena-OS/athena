# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-gensim
_pkgname=${pkgname#python2-}
pkgver=3.8.3
pkgrel=1
pkgdesc='Python framework for fast Vector Space Modelling.'
arch=('any')
url='https://pypi.org/project/gensim/#files'
license=('LGPL')
depends=('python2' 'python2-numpy' 'python2-scipy' 'python2-smart-open')
makedepends=('python2-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('5599c1fed4c2826f1be10420812369b16320a45f7e896e623fe302984f8eb80f5de45aeaa44a67b24eedb3b5a09f6368b3fd717f07b8435fad7e54a4de6c4061')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}
