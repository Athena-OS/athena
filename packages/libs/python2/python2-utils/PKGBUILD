# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=python2-utils
_pkgname=python-utils
pkgver=2.7.1
pkgrel=1
pkgdesc='A module with some convenient utilities not included with the standard Python install.'
url='https://github.com/WoLpH/python-utils'
license=('BSD')
arch=('any')
depends=('python2' 'python2-six')
makedepends=('python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('9f74a33089886dcd28dfd45602c53a7c6cfc90b151a053dd7e49a5fd502479e889d08bd8b10fdd2ec3bc26e334ae66238740ff59291f4ac95fb486b64f925d4a')

build() {
  cd "$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

