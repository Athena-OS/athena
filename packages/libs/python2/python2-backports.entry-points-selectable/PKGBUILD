# Credits BlackArch ( https://www.blackarch.org/ ).
# See COPYING for license details.

# This file was taken in modified form from AUR
# AUR Maintainer: Felix Yan <felixonmars@archlinux.org>
# AUR Contributor: Crass00 <crass00 @ hotmail . com>

pkgname=python2-backports.entry-points-selectable
_pkgname=backports.entry_points_selectable
pkgver=1.1.1
_pyver=2.7
pkgrel=1
pkgdesc='Compatibility shim providing selectable entry points for older implementations.'
arch=('any')
url='https://github.com/jaraco/backports.entry_points_selectable'
license=('MIT')
depends=('python2-backports' 'python2-pluggy' 'python2-funcsigs')
makedepends=('python2-setuptools' 'git')
source=("https://github.com/jaraco/$_pkgname/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('b811a473e13ebb3dafe94d726bfe98d7569a9d9a98ab77f1ed975b17deecf84e93e04a4ee263c3721b4e9fbbfae2f307b8c88a83441f792dd88511bb99f19c1d')

build() {
  cd $_pkgname-$pkgver

  python2 setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE

  rm "$pkgdir/usr/lib/python$_pyver/site-packages/backports/__init__.py"*
}

