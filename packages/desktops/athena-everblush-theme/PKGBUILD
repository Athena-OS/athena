pkgname=athena-everblush-theme
pkgver=r26.eee79de
pkgrel=1
pkgdesc="Everblush GTK theme for Athena."
arch=('any')
license=("GPL3")
depends=()
makedepends=('git' 'npm')
install='everblush-theme.install'
source=("git+https://github.com/Everblush/gtk"
        "git+https://github.com/alvatip/Nordzy-icon"
        "git+https://github.com/alvatip/Radioactive-nord"
        "git+https://github.com/Everblush/wallpapers"
        "https://raw.githubusercontent.com/kovidgoyal/kitty-themes/master/themes/Atom.conf"
        "mechanic-keyboard.png"
        "$pkgname.tar.gz")
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'a23c6f6e71c544110aa576a57174145a72eb7d2389a3927c154a7cb528fa2fb4a6baffd4f1287f53edcbf2553827af271acb24cdbc1d7369fc2820810f4194e4'
            'f39efd5289beacf4e5bd0165c057aaa4904a034a7c3bbc8a563f244e5bcf534f2e902b1b1d24c461e6f569223df6905ac2ab95555e3fded4bc3d93bdff945599'
            'c8c0d9e9415bb6b91b5eec4ae20bb568fe5379ce629c471c698119c68a3ddc99158eeeaa4a1941d2697764b8f51c7882e9252063de72559c35e7a537c804b310')

pkgver() {
    cd gtk

    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)" 
}

package() {
    cd gtk

    _npmdir="$pkgdir/usr/lib/node_modules/"

    install -dm 755 "$_npmdir"

    cd "$_npmdir"

    npm install --user root -g --prefix "$pkgdir/usr" scss

    cd "$srcdir/gtk"

    # Install Everblush theme in /usr/share/themes
    make DESTDIR="$pkgdir" install

    # Install icon theme
    cd "${srcdir}/Nordzy-icon"
    install -dm755 "${pkgdir}/usr/share/icons"
    ./install.sh -d "${pkgdir}/usr/share/icons" -t cyan -c dark

    # Install cursor theme
    cd "${srcdir}/Radioactive-nord"
    cp -a Radioactive-nord "${pkgdir}/usr/share/icons/"

    # Install wallpapers
    cd "${srcdir}/wallpapers"
    install -dm 755 "${pkgdir}/usr/share/wallpapers/Everblush"
    cp -a *.png "${pkgdir}/usr/share/wallpapers/Everblush/"

    cd "${srcdir}"
    install -dm 755 "$pkgdir/etc/skel/.config/kitty"
    install -dm 755 "$pkgdir/usr/share/athena-everblush-theme"
    install -dm 755 "$pkgdir/usr/share/backgrounds/default"

    install -Dm 644 "mechanic-keyboard.png" "$pkgdir/usr/share/backgrounds/default/"
    install -Dm 644 "Atom.conf" "$pkgdir/etc/skel/.config/kitty/"

    cp -a "$srcdir/icons/apps" "$pkgdir/usr/share/athena-everblush-theme/"
    cp -a "$srcdir/icons/categories" "$pkgdir/usr/share/athena-everblush-theme/"
}
