pkgname=matrix-gtk-theme
_pkgname=Matrix-GTK-Theme
pkgver=12.15a799b
pkgrel=1
pkgdesc='A GTK theme based on the Matrix colour palette.'
arch=('any')
license=('GPL3')
makedepends=('git')
source=("git+https://github.com/D3vil0p3r/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

package() {
  cd $_pkgname/themes
  
  install -dm 755 "$pkgdir/usr/share/themes"
  install -dm 755 "$pkgdir/usr/share/icons"

  ./install.sh -l --tweaks macos -d "$pkgdir/usr/share/themes"

  cp -a ../icons/Matrix-Dark ../icons/Matrix-Light ../icons/Matrix-Sweet "$pkgdir/usr/share/icons/"
}
