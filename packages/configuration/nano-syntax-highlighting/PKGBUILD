# Contributor: American_Jesus <american.jesus.pt AT gmail DOT com>
# Maintainer: Matthew Sexton <wsdmatty@gmail.com>
pkgname=nano-syntax-highlighting
pkgver=2020.10.10.r10.g1aa64a8
pkgrel=1
pkgdesc="improved nano syntax highlighting files."
arch=('any')
depends=('nano')
makedepends=('git')
url="https://github.com/scopatz/nanorc"
license=('GPL-3.0-or-later')
install=nano-syntax-highlighting.install
provides=('nano-syntax-highlighting')
conflicts=('nano-syntax-highlighting')
source=('nano-syntax-highlighting::git+https://github.com/scopatz/nanorc.git')
sha512sums=('SKIP')

pkgver() {
	cd "${srcdir}/${pkgname%-git}"

        git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	# Generate nanorc.sample
	find "${srcdir}"/"${pkgname%-git}"/*.nanorc -type f | sed "s@${srcdir}\/${pkgname%-git}@include \/usr\/share\/${pkgname%-git}@" > "${srcdir}/nanorc.sample"
}

package() {
	mkdir -p "${pkgdir}/usr/share/${pkgname%-git}"
        mkdir -p "${pkgdir}/etc"

	find "${srcdir}"/"${pkgname%-git}"/ -name '*.nanorc' | xargs install -D -m644 -t "${pkgdir}/usr/share/${pkgname%-git}/"

	install -D -m644 "${srcdir}/nanorc.sample" "${pkgdir}/usr/share/${pkgname%-git}/nanorc.sample"

        sed -i 's/icolor brightnormal " brightnormal"/icolor normal " brightnormal"/g' "${pkgdir}/usr/share/nano-syntax-highlighting/nanorc.nanorc"
}
