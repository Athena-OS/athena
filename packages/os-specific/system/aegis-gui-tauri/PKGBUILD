pkgname=aegis-gui-tauri
pkgver=139.f3e1f16
pkgrel=1
pkgdesc='Tauri-based GUI front-end for Aegis.'
arch=('x86_64' 'aarch64')
url='https://github.com/Athena-OS/aegis-gui-tauri'
license=('GPL-3.0-or-later')
depends=('gtk3' 'libappindicator-gtk3' 'libsoup' 'webkit2gtk')
makedepends=('cargo-tauri' 'pnpm' 'yarn' 'git')
options=('!strip' '!debug')
source=("git+https://github.com/mwaurawakati/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
    cd $pkgname
    
    cargo tauri build
}

package() {
    cd $pkgname
    install -Dm 755 "src-tauri/target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
}
