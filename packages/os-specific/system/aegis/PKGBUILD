pkgname=aegis
pkgver=396.bff5840
pkgrel=1
pkgdesc='Scriptable backend installer for Athena OS.'
arch=('x86_64' 'aarch64')
url='https://github.com/Athena-OS/aegis'
license=('GPL-3.0-or-later')
depends=('parted' 'arch-install-scripts' 'btrfs-progs' 'dosfstools' 'e2fsprogs' 'f2fs-tools' 'netcat' 'nix' 'ntfs-3g' 'rate-mirrors' 'unzip' 'util-linux' 'whois' 'xfsprogs')
makedepends=('cargo' 'git')
source=("git+https://github.com/Athena-OS/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
    cd $pkgname

    cargo fetch --locked --target "${CARCH}-unknown-linux-gnu"
}

build() {
    cd $pkgname

    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

package() {
    cd $pkgname
    
    install -Dm 755 "target/release/$pkgname" "${pkgdir}/usr/bin/$pkgname"
}
