pkgname=cyber-toolkit
pkgver=30.5742bc0
pkgrel=1
pkgdesc='Set your Cyber Security role.'
arch=('x86_64' 'aarch64')
url='https://github.com/Athena-OS/cyber-toolkit'
license=('MIT')
depends=('rate-mirrors')
makedepends=('git' 'cargo')
source=("git+https://github.com/Athena-OS/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  ( set -o pipefail
    git describe --long --tags --abbrev=7 2>/dev/null |
      sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "%s.%s" "$(git rev-list --count HEAD)" \
      "$(git rev-parse --short=7 HEAD)"
  )
}

build() {
  cd $pkgname

  cargo clean
  cargo build --release --locked
}

package() {
  cd "$pkgname"

  install -dm 755 "$pkgdir/usr/share/$pkgname"
  install -dm 755 "$pkgdir/usr/share/$pkgname/roles"

  install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  for f in roles/*.role; do
    [ -f "$f" ] || continue
    install -Dm 644 "$f" "$pkgdir/usr/share/$pkgname/roles/$(basename "$f")"
  done
}
