# Maintainer: zxp19821005 <zxp19821005 at 163 dot com>
pkgname=kando-bin
_pkgname=Kando
pkgver=2.0.0
_electronversion=31
pkgrel=2
pkgdesc='A pie menu for the desktop. It will be highly customizable and will allow you to create your own menus and actions.'
arch=('aarch64' 'x86_64')
url="https://ko-fi.com/post/Introducing-Ken-Do-L3L7L0FQ2"
_ghurl="https://github.com/kando-menu/kando"
license=("MIT")
provides=("${pkgname%-bin}=${pkgver}")
conflicts=("${pkgname%-bin}")
depends=(
    "electron${_electronversion}"
    'xdg-utils'
)
optdepends=(
    'kde-cli-tools'
    'gnome-browser-connector'
    'trash-cli'
    'gvfs'
    'alsa-lib'
    'pulseaudio'
)
options=(
    '!strip'
    '!emptydirs'
)
source=(
    "LICENSE-${pkgver}::https://raw.githubusercontent.com/kando-menu/kando/v${pkgver}/LICENSE.md"
    "${pkgname%-bin}.sh"
    "config.json"
    "menus.json"
)
source_aarch64=("${pkgname%-bin}-${pkgver}-aarch64.rpm::${_ghurl}/releases/download/v${pkgver}/${pkgname%-bin}-${pkgver}-1.arm64.rpm")
source_x86_64=("${pkgname%-bin}-${pkgver}-x86_64.rpm::${_ghurl}/releases/download/v${pkgver}/${pkgname%-bin}-${pkgver}-1.x86_64.rpm")
sha512sums=('046d3429e40271479fcdd731c793ea9c20533d1be46701ced689925778b8949f0b87f4b1b1d0f231428f4524141c9e4c885be632e382a538f6cca25137cec3a1'
            '3578476802bbcd6680830bd3853e76c4999bed73fce55ff955baf82cee24204c8b5639ff3e2ca915147caed1ddf9d8380923ca37bf80c138e524da2456dfe0cf'
            '81064df4033091749b989a1be6c2be4ebc1fa63ab59c893cfb9ce806c0da9c8b531e3c218561909568444fe41d1592722ef7f5dd53e7f9159e4cf6e49659656b'
            '3fcca1eac04d75b71d9beaa5eeca39e4015e26e6a396cb200133f8021dbf8fbd7b6a4f294c106752e830a140e014758e582d6f31ccb2f869647fb9c891429110')
sha512sums_aarch64=('628f5b89875129d4b0ae62a72c7c617be7c46cf810af12622e389d4c8c3e5a4675745f350511d0071b5bda55f44d50395ec641d3193e9251cce061e24cd3cb38')
sha512sums_x86_64=('ce85cbbeaef3dd80230c867b93d2011c2c6996324a7b54b0a4e87ba74df43d5e9748b52536e6dc124e4a1b36735d008a49ef35cab64fcb7495541b41f6dbc198')
prepare() {
    sed -i -e "
        s/@electronversion@/${_electronversion}/
        s/@appname@/${pkgname%-bin}/
        s/@runname@/app/
        s/@cfgdirname@/${_pkgname}/
        s/@options@/env ELECTRON_OZONE_PLATFORM_HINT=auto/
    " "${srcdir}/${pkgname%-bin}.sh"
}
package() {
    install -Dm755 "${srcdir}/${pkgname%-bin}.sh" "${pkgdir}/usr/bin/${pkgname%-bin}"
    install -dm755 "${pkgdir}/etc/skel/.config/${pkgname%-bin}"
    install -dm755 "${pkgdir}/etc/xdg/autostart"
    install -dm755 "${pkgdir}/usr/lib/${pkgname%-bin}"
    install -Dm644 "config.json" "${pkgdir}/etc/skel/.config/${pkgname%-bin}/config.json"
    install -Dm644 "menus.json" "${pkgdir}/etc/skel/.config/${pkgname%-bin}/menus.json"
    cp -Pr --no-preserve=ownership "${srcdir}/usr/lib/${pkgname%-bin}/resources/app" "${pkgdir}/usr/lib/${pkgname%-bin}"
    install -dm755 "${pkgdir}/usr/lib/${pkgname%-bin}/app/.webpack/renderer/assets/icon-themes"
    ln -s "/usr/share/icons/hicolor" "${pkgdir}/usr/lib/${pkgname%-bin}/app/.webpack/renderer/assets/icon-themes/hicolor"
    ln -s "/usr/share/icons/htb-toolkit" "${pkgdir}/usr/lib/${pkgname%-bin}/app/.webpack/renderer/assets/icon-themes/htb-toolkit"
    ln -s "/usr/share/pixmaps" "${pkgdir}/usr/lib/${pkgname%-bin}/app/.webpack/renderer/assets/icon-themes/pixmaps"
    install -Dm644 "${srcdir}/usr/share/applications/${pkgname%-bin}.desktop" -t "${pkgdir}/etc/xdg/autostart"
    install -Dm644 "${srcdir}/usr/share/applications/${pkgname%-bin}.desktop" -t "${pkgdir}/usr/share/applications"
    install -Dm644 "${srcdir}/usr/share/pixmaps/${pkgname%-bin}.png" -t "${pkgdir}/usr/share/pixmaps"
    install -Dm644 "${srcdir}/LICENSE-${pkgver}" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
