# Credits BlackArch ( https://www.blackarch.org/ ).
#
# This PKGBUILD was extracted from AUR.
# Creator: Dave Reisner <d@falconindy.com>
# Changes by: psf

pkgname=cower
pkgver=3.0.4.r2.g1c36721
pkgrel=1
pkgdesc='A simple AUR agent with a pretentious name.'
arch=('x86_64' 'aarch64')
url='https://github.com/mgalgs/cower'
license=('MIT')
depends=('curl' 'pacman' 'yajl')
makedepends=('perl')
source=("git+https://github.com/mgalgs/$pkgname")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  make
  sed '/^$/q' cower.c > LICENSE
}

package() {
  cd $pkgname

  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

